# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gicu Gorodenco <cyclopsihus@gmail.com>
# Contributor: rafael ff1 <rafael.f.f1@gmail.com>

pkgname=lib32-libdaemon
pkgver=0.14
pkgrel=3
pkgdesc='A lightweight C library which eases the writing of UNIX daemons'
arch=('x86_64')
url="http://0pointer.de/lennart/projects/${pkgname#*-}/"
license=('LGPL')
depends=('lib32-glibc' "${pkgname#*-}=${pkgver}")
makedepends=('gcc-multilib')
source=("${url}/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('fd23eb5f6f986dcc7e708307355ba3289abe03cc381fc47a80bca4a50aa6b834')

build() {
  cd ${pkgname#*-}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --localstatedir='/var' \
              --disable-lynx
  make
}

package() {
  cd ${pkgname#*-}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{include,share}
}

# vim: ts=2 sw=2 et:
