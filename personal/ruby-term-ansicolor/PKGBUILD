# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Hyacinthe Cartiaux <hyacinthe dot cartiaux at free dot fr>
# Contributor: Hilton Medeiros <medeiros dot hilton at gmail dot com>

pkgname=ruby-term-ansicolor
pkgver=1.2.2
pkgrel=2
pkgdesc="A ruby library that colors strings using ANSI escape sequences"
arch=('any')
url="https://rubygems.org/gems/term-ansicolor"
license=('GPL2')
depends=('ruby-tins>=0.8')
source=("https://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem")
noextract=("${pkgname#*-}-${pkgver}.gem")
sha256sums=('b9534713ba4c2a48f2291fadf9ce2edae9f2dc96157ee812fe8fe16a70c081ac')

prepare() {
  gem install --no-{document,user-install} --ignore-dependencies -i . ${pkgname#*-}-${pkgver}.gem
}

package() {
  cd gems/${pkgname#*-}-${pkgver}

  local _rubyver="$(ruby --version | sed 's/.* \(.*\..*\..*\)p.*/\1/')"

  install -dm 755 "${pkgdir}"/usr/lib/ruby/{gems/${_rubyver},vendor_ruby}
  mv bin "${pkgdir}"/usr/
  mv lib "${pkgdir}"/usr/lib/ruby/vendor_ruby/${_rubyver}

# Gem compatibility
  mv ../../specifications "${pkgdir}"/usr/lib/ruby/gems/${_rubyver}/
}

# vim: ts=2 sw=2 et:
