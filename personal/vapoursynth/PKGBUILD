# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile.desu@gmail.com>

pkgname=vapoursynth
pkgver=R22
pkgrel=4
pkgdesc='A video processing framework with the future in mind'
arch=('i686' 'x86_64')
url='http://www.vapoursynth.com/'
license=('LGPL2.1' 'custom:OFL')
depends=('ffmpeg' 'python')
makedepends=('cython' 'python-sphinx' 'waf' 'yasm')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('e6e87e05bbefc87206e7bdad1fabd4b404f5ddabf98919b90115814ba5515656')

build() {
  cd ${pkgname}-${pkgver}

  waf configure --prefix='/usr' --docs='true' --examples='true'
  waf build
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}

  waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize='1'
  cp -dr --no-preserve='ownership' test "${pkgdir}"/usr/share/${pkgname}/

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 ofl.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
