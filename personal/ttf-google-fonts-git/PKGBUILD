# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Sebastian Stammler <stammler.s@gmail.com>
# Contributor: Sarkasper <kasper.menten@gmx.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Michalis Georgiou <mechmg93@gmail.comr>
# Contributor: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=ttf-google-fonts-git
pkgver=r12.7bd0368
pkgrel=1
pkgdesc='TrueType fonts from the Google Fonts project'
arch=('any')
url='https://github.com/google/fonts'
license=('Apache' 'custom:OFL' 'custom:UFL')
depends=('cantarell-fonts' 'fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontdir'
         'xorg-mkfontscale')
makedepends=('git')
provides=('adobe-source-code-pro-fonts' 'adobe-source-sans-pro-fonts'
          'googlefontdirectory' 'jsmath-fonts' 'lohit-fonts' 'oldstand-font'
          'openarch_fonts' 'otf-bitter' 'otf-goudy' 'ttf-andika'
          'ttf-anonymous-pro' 'ttf-cantarell' 'ttf-cardo' 'ttf-chromeos-fonts'
          'ttf-comfortaa' 'ttf-droid' 'ttf-fira-mono' 'ttf-fira-sans'
          'ttf-font' 'ttf-google-fonts-hg' 'ttf-google-webfonts'
          'ttf-google-webfonts-git' 'ttf-google-webfonts-hg'
          'ttf-inconsolata' 'ttf-kimberly_geswein_print' 'ttf-lato'
          'ttf-lato' 'ttf-lekton' 'ttf-medievalsharp' 'ttf-nova'
          'ttf-oldstandard' 'ttf-opensans' 'ttf-oxygen' 'ttf-oxygen-git'
          'ttf-pt-mono' 'ttf-ptsans' 'ttf-pt-sans' 'ttf-roboto' 'ttf-sil-fonts'
          'ttf-sortsmillgoudy' 'ttf-source-code-pro' 'ttf-source-sans-pro'
          'ttf-ubuntu-font-family' 'ttf-vollkorn')
conflicts=('adobe-source-code-pro-fonts' 'adobe-source-sans-pro-fonts'
           'googlefontdirectory' 'jsmath-fonts' 'lohit-fonts' 'oldstand-font'
           'openarch_fonts' 'otf-bitter' 'otf-goudy' 'ttf-andika'
           'ttf-anonymous-pro' 'ttf-cantarell' 'ttf-cardo' 'ttf-chromeos-fonts'
           'ttf-comfortaa' 'ttf-droid' 'ttf-fira-mono' 'ttf-fira-sans'
           'ttf-google-fonts-hg' 'ttf-google-webfonts'
           'ttf-google-webfonts-git' 'ttf-google-webfonts-hg' 'ttf-inconsolata'
           'ttf-kimberly_geswein_print' 'ttf-lato' 'ttf-lato' 'ttf-lekton'
           'ttf-medievalsharp' 'ttf-nova' 'ttf-oldstandard' 'ttf-opensans'
           'ttf-oxygen' 'ttf-oxygen-git' 'ttf-pt-mono' 'ttf-ptsans'
           'ttf-pt-sans' 'ttf-roboto' 'ttf-sil-fonts' 'ttf-sortsmillgoudy'
           'ttf-source-code-pro' 'ttf-source-sans-pro' 'ttf-ubuntu-font-family'
           'ttf-vollkorn')
install='ttf-google-fonts.install'
source=('ttf-google-fonts::git+https://github.com/google/fonts.git'
        'OFL.txt'
        'UFL.txt')
sha256sums=('SKIP'
            'ca122f3bda0154f692817450168cb650a7fc59ef96c93582acd2e7a744d464d3'
            '2f0015108d68627bd788d313f529c21ff4da2c2c42a5e1f3883acc83480f9002')

pkgver() {
  cd ttf-google-fonts

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

package() {
  cd ttf-google-fonts

  install -dm 755 "${pkgdir}"/usr/share/fonts/TTF
  find . -type f -name *.ttf -exec install -m 644 {} "${pkgdir}"/usr/share/fonts/TTF/ \;
  find "${pkgdir}" -type f -name Cantarell-*.ttf -delete
  find "${pkgdir}" -type f -name Khand-*.ttf -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/ttf-google-fonts-git
  install -m 644 ../OFL.txt ../UFL.txt "${pkgdir}"/usr/share/licenses/ttf-google-fonts-git/
}

# vim: ts=2 sw=2 et:
