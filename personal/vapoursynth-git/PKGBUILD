# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile.desu@gmail.com>

pkgname=vapoursynth-git
pkgver=R24.r7.4bfbaf7
pkgrel=1
pkgdesc='A video processing framework with the future in mind'
arch=('i686' 'x86_64')
url='http://www.vapoursynth.com/'
license=('LGPL2.1' 'custom:OFL')
depends=('ffmpeg' 'python' 'tesseract')
makedepends=('cython' 'git' 'python-sphinx' 'waf' 'yasm')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('git+https://github.com/vapoursynth/vapoursynth.git')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "%s" "$(git describe --tags | sed 's/-/.r/; s/-g/./')"
}

build() {
  cd ${pkgname%-*}

  waf configure --prefix='/usr' --enable-examples
  waf build $MAKEFLAGS
  python setup.py build
}

package() {
  cd ${pkgname%-*}

  waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize='1'

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 ofl.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
