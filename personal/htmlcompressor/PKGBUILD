# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Peter A. Shevtsov <pshevtsov@severusweb.ru>

pkgname=htmlcompressor
pkgver=1.5.3
pkgrel=3
pkgdesc='HTML Compressor and Minifier'
arch=('any')
url="http://code.google.com/p/${pkgname}/"
license=('APACHE')
depends=('java-runtime')
optdepends=('closure-compiler: Inline JavaScript compression'
            'yuicompressor: Inline CSS and JavaScript compression')
source=("http://${pkgname}.googlecode.com/files/${pkgname}-${pkgver}.jar")
sha256sums=('88894e330cdb0e418e805136d424f4c262236b1aa3683e51037cdb66310cb0f9')

package() {
  install -dm 755 "${pkgdir}"/usr/{bin,share/java/${pkgname}}
  install -m 755 ${pkgname}-${pkgver}.jar "${pkgdir}"/usr/share/java/${pkgname}/${pkgname}.jar

  echo '#!/bin/sh
  "$JAVA_HOME/bin/java" -jar /usr/share/java/htmlcompressor/htmlcompressor.jar $@' > "${pkgdir}"/usr/bin/${pkgname}
  chmod +x "${pkgdir}"/usr/bin/${pkgname}

  ln -s ../closure-compiler/closure-compiler.jar "${pkgdir}"/usr/share/java/${pkgname}/compiler.jar
  ln -s ../yuicompressor/yuicompressor.jar "${pkgdir}"/usr/share/java/${pkgname}/yuicompressor.jar
}

# vim: ts=2 sw=2 et:
