# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lekensteyn <lekensteyn@gmail.com>
# Contributor: Samsagax <samsagax@gmail.com>

pkgname=bbswitch-dkms
pkgver=0.8
pkgrel=1
pkgdesc='kernel module allowing to switch dedicated graphics card on Optimus laptops'
arch=('any')
url=('http://github.com/Bumblebee-Project/bbswitch')
license=('GPL')
depends=('dkms')
optdepends=('linux-headers: Build the module for Arch kernel'
            'linux-lts-headers: Build the module for LTS Arch kernel')
provides=('bbswitch')
conflicts=('bbswitch' 'bbswitch-git')
install='bbswitch-dkms.install'
source=("https://github.com/Bumblebee-Project/bbswitch/archive/v${pkgver}.tar.gz"
        'dkms.conf.in')
sha256sums=('76cabd3f734fb4fe6ebfe3ec9814138d0d6f47d47238521ecbd6a986b60d1477'
            '61917827739b518401ec64d842e9e851d070bd28492172d6c22c9031e52de1a1')

package() {
  cd bbswitch-${pkgver}

  install -dm 755 "${pkgdir}"/usr/src/bbswitch-${pkgver}
  install -m 644 Makefile "${pkgdir}"/usr/src/bbswitch-${pkgver}/
  install -m 644 bbswitch.c "${pkgdir}"/usr/src/bbswitch-${pkgver}/
  sed "s/#MODULE_VERSION#/${pkgver}/" ../dkms.conf.in > "${pkgdir}"/usr/src/bbswitch-${pkgver}/dkms.conf
}

# vim: ts=2 sw=2 et:
