# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: PelPix <kylebloss@pelpix.info>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=x264-10bit-git
pkgver=r2389.956c8d8
pkgrel=1
pkgdesc='Library for encoding H264/AVC video streams'
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/x264.html'
license=('GPL')
depends=('ffms2-git' 'l-smash-git')
makedepends=('yasm' 'git')
source=('git://git.videolan.org/x264.git')
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%%-*}

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%%-*}

  ./configure --prefix='/usr' --bit-depth='10'
  make
}

package() {
  cd ${pkgname%%-*}

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/bin/x264{,-10bit}
}

# vim: ts=2 sw=2 et:
