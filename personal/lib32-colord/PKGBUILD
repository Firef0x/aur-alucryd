# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-colord
pkgver=1.2.3
pkgrel=1
pkgdesc='System daemon for managing color devices (32-bit)'
arch=('x86_64')
url='http://www.freedesktop.org/software/colord'
license=('GPL2')
depends=("colord=${pkgver}" 'lib32-dbus' 'lib32-dconf' 'lib32-glib2'
         'lib32-gusb' 'lib32-lcms2' 'lib32-polkit' 'lib32-sqlite'
         'lib32-systemd')
makedepends=('gcc-multilib' 'gobject-introspection' 'intltool' 'vala')
source=("http://www.freedesktop.org/software/colord/releases/colord-${pkgver}.tar.xz")
sha256sums=('61ceaff621a8d8d5b6171d04f134bfff0a4f2c53')

build() {
  cd colord-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' \
              --libdir='/usr/lib32' --libexecdir='/usr/lib32/colord' \
              --with-systemdsystemunitdir='/usr/lib/systemd/system' \
              --disable-{bash-completion,nls,static,volume-search} \
              --enable-vala --with-daemon-user='colord'
  make
}

package() {
  cd colord-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,var,usr/{bin,include,lib,share}}
}

# vim: ts=2 sw=2 et:
