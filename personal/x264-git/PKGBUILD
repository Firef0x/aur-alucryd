# Maintainer : Alucryd <alucryd at gmail dot com>

pkgname=x264-git
pkgver=2334
pkgrel=1
pkgdesc="Free library for encoding H264/AVC video streams"
arch=('i686' 'x86_64')
url="http://www.videolan.org/developers/x264.html"
license=('GPL')
depends=('glibc')
makedepends=('yasm' 'git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('git://git.videolan.org/x264.git')
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  git rev-list --count master
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
