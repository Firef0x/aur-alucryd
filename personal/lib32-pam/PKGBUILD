# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Janax <janax99@yahoo.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=lib32-pam
pkgver=1.1.8
pkgrel=5
pkgdesc='PAM (Pluggable Authentication Modules)'
arch=('x86_64')
url='http://linux-pam.org'
license=('GPL2')
depends=('lib32-cracklib' 'lib32-libtirpc' "pam=${pkgver}")
makedepends=('gcc-multilib' 'lib32-flex')
source=("https://fedorahosted.org/releases/l/i/linux-pam/Linux-PAM-${pkgver}.tar.bz2"
        'ftp://ftp.archlinux.org/other/pam_unix2/pam_unix2-2.9.1.tar.bz2'
        'pam_unix2-glibc216.patch'
        'pam-1.1.8-cve-2013-7041.patch'
        'pam-1.1.8-cve-2014-2583.patch')
sha256sums=('c4b1f23a236d169e2496fea20721578d864ba00f7242d2b41d81050ac87a1e55'
            '3315747699fece4e1cc5771885d243b3e017c4c4ca1326e86228d590a840e955'
            '6644c5cff46878c65bdc77977becbeda392675702264bfcc7c610a45a9982574'
            '18034730d74f67c79feb2c6abc796442c0c548ce221b8e9d633e98a7bd3cce2c'
            'b10255f690f9f4a8cec044383d9cd03031b9a7be9892d824b2adc451d6d06f65')
options=('!emptydirs')

prepare () {
  cd Linux-PAM-${pkgver}

  patch -Np1 -i ../pam-1.1.8-cve-2013-7041.patch
  patch -Np1 -i ../pam-1.1.8-cve-2014-2583.patch

  cd ../pam_unix2-2.9.1

  patch -Np1 -i ../pam_unix2-glibc216.patch
}

build() {
  cd Linux-PAM-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --libdir='/usr/lib32' --sbindir='/usr/bin' \
              --with-xml-catalog='no' --disable-db
  make

  cd ../pam_unix2-2.9.1

  export CFLAGS="$CFLAGS -I"${srcdir}"/Linux-PAM-${pkgver}/libpam/include/"
  export LDFLAGS="$LDFLAGS -L"${srcdir}"/Linux-PAM-${pkgver}/libpam/.libs/"

  ./configure --sbindir='/usr/bin' --libdir='/usr/lib32'
  make
}

package() {
  cd Linux-PAM-${pkgver}

  make DESTDIR="${pkgdir}" SCONFIGDIR='/etc/security' install
  rm -rf "${pkgdir}"/{etc,usr/{include,share,bin}}

  cd ../pam_unix2-2.9.1

  install -m 644 src/pam_unix2.so "${pkgdir}"/usr/lib32/security/

  cd "${pkgdir}"/usr/lib32/security
  ln -s pam_unix.so pam_unix_acct.so
  ln -s pam_unix.so pam_unix_auth.so
  ln -s pam_unix.so pam_unix_passwd.so
  ln -s pam_unix.so pam_unix_session.so
}

# vim: ts=2 sw=2 et:
