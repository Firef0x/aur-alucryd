# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Emiliano Vavassori <syntaxerrormmm at gmail dot com>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

pkgname=ruby-mail
pkgver=2.5.4
pkgrel=1
pkgdesc="A really Ruby Mail handler"
arch=('any')
url="http://github.com/mikel/mail"
license=('MIT')
depends=('ruby-mime-types>=1.16' 'ruby-treetop>=1.4.8')
source=("http://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem")
noextract=("${pkgname#*-}-${pkgver}.gem")
sha256sums=('446585c38b062121252688dcc9cc70af1f470822e30db021bb97d185969e257c')

build() {
  gem install --no-user-install --ignore-dependencies -i . ${pkgname#*-}-${pkgver}.gem
}

package() {
  local _rubyver="$(ruby --version | sed 's/.* \(.*\..*\..*\)p.*/\1/')"
  install -dm 755 "${pkgdir}"/usr/lib/ruby/{gems,vendor_ruby}/${_rubyver}
  mv gems/${pkgname#*-}-${pkgver}/lib/* "${pkgdir}"/usr/lib/ruby/vendor_ruby/${_rubyver}/
  mv doc specifications "${pkgdir}"/usr/lib/ruby/gems/${_rubyver}/

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 {gems/${pkgname#*-}-${pkgver},"${pkgdir}"/usr/share/licenses/${pkgname}}/MIT-LICENSE
}

# vim: ts=2 sw=2 et:
