# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=vapoursynth-plugin-f3kdb-git
pkgver=2.0.r399.8ea4292
pkgrel=1
pkgdesc='flash3kyuu deband plugin for VapourSynth'
arch=('i686' 'x86_64')
url='https://github.com/SAPikachu/flash3kyuu_deband'
license=('GPL3')
depends=('vapoursynth')
makedepends=('git' 'waf')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "2.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-*}

  waf configure --prefix='/usr' --libdir='/usr/lib/vapoursynth'
  waf build
}

package() {
  cd ${pkgname%-*}

  waf install --destdir="${pkgdir}" --no-ldconfig
}

# vim: ts=2 sw=2 et:
