# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-lsmashsource-git
pkgver=r708.c7cc104
pkgrel=1
pkgdesc='L-SMASH source plugin for Vapoursynth'
arch=('i686' 'x86_64')
url='https://github.com/VFR-maniac/L-SMASH-Works'
license=('GPL')
depends=('l-smash' 'vapoursynth')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-*}/VapourSynth

  ./configure --prefix='/usr' --extra-cflags='-fPIC'
  make
}

package(){
  cd ${pkgname%-*}/VapourSynth

  install -dm 755 "${pkgdir}"/usr/{lib/vapoursynth,share/doc/vapoursynth/plugins/lsmashsource}
  install -m 755 vslsmashsource.so.* "${pkgdir}"/usr/lib/vapoursynth/vslsmashsource.so
  install -m 644 README "${pkgdir}"/usr/share/doc/vapoursynth/plugins/lsmashsource/
}

# vim: ts=2 sw=2 et:
