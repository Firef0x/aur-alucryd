# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-bsnes-git
pkgver=r424.4a98250
pkgrel=1
pkgdesc='libretro implementation of bSNES/higan (Super Nintendo Entertainment System)'
arch=('i686' 'x86_64')
url='https://github.com/libretro/bsnes-libretro'
license=('GPL3')
depends=('gcc-libs')
makedepends=('git')

source=('git+https://github.com/libretro/bsnes-libretro.git'
        'https://raw.github.com/libretro/libretro-super/master/dist/info/bsnes_accuracy_libretro.info'
        'https://raw.github.com/libretro/libretro-super/master/dist/info/bsnes_performance_libretro.info'
        'https://raw.github.com/libretro/libretro-super/master/dist/info/bsnes_balanced_libretro.info')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd bsnes-libretro

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd bsnes-libretro

  for profile in {accuracy,balanced,performance}; do
    git clone . ${profile}
    pushd ${profile}
    make ui=target-libretro profile=${profile}
    popd
  done
}

package() {
  cd bsnes-libretro

  install -dm 755 "${pkgdir}"/usr/lib/libretro
  for profile in {accuracy,balanced,performance}; do
    install -m 644 ${profile}/out/bsnes_libretro.so "${pkgdir}"/usr/lib/libretro/libretro-bsnes-${profile}.so
    install -m 644 ../bsnes_${profile}_libretro.info "${pkgdir}"/usr/lib/libretro/libretro-bsnes-${profile}.info
  done
}

# vim: ts=2 sw=2 et:
