# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>

pkgname=lib32-gtk-engine-murrine
pkgver=0.98.2
pkgrel=1
pkgdesc='Murrine GTK2 engine'
arch=('x86_64')
url='http://cimitan.com/murrine/project/murrine'
license=('GPL')
depends=('lib32-gtk2' "${pkgname#*-}")
makedepends=('gcc-multilib' 'intltool' 'perl')
source=("http://ftp.gnome.org/pub/GNOME/sources/murrine/0.98/murrine-${pkgver}.tar.xz") 
sha256sums=('e9c68ae001b9130d0f9d1b311e8121a94e5c134b82553ba03971088e57d12c89')

build() {
  cd murrine-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --enable-animation{,rtl}
  make
}

package() {
  cd murrine-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share
}

# vim: ts=2 sw=2 et:
