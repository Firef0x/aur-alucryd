# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Gordin <gordin@koeln.ccc.de>

pkgname=wit
pkgver=2.29a
pkgrel=1
pkgdesc='Wiimms ISO Tools'
arch=('i686' 'x86_64')
url='http://wit.wiimm.de'
license=('GPL2')
depends=('bash' 'fuse' 'wget')
makedepends=('subversion')
install='wit.install'
source=('wit::svn+http://opensvn.wiimm.de/wii/trunk/wiimms-iso-tools#revision=5186')
sha256sums=('SKIP')

prepare() {
  cd wit

  sed 's|@@INSTALL-PATH@@|/usr|; s|@@SHARE-PATH@@|/usr/share/wit|' -i setup/load-titles.sh
}

build() {
  cd wit

  export LDFLAGS="$LDFLAGS,-z,noexecstack"

  make INSTALL_PATH="${pkgdir}/usr" tools
  make doc
}

package () {
  cd wit

  make install
  install -dm 755 "${pkgdir}"/usr/share/doc/wit
  install -m 644 doc/*.txt "${pkgdir}"/usr/share/doc/wit/
}

# vim: ts=2 sw=2 et:
