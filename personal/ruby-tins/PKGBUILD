# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=ruby-tins
pkgver=0.10.0
pkgrel=1
pkgdesc="All the stuff that isn't good/big enough for a real library"
arch=('any')
url="https://github.com/flori/tins"
license=('MIT')
depends=('ruby')
source=("https://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem")
noextract=("${pkgname#*-}-${pkgver}.gem")
sha256sums=('b6cc4ea8c05cc5949326b6ff6205d4d8369835d9b7e5694e1a05888e4cccd5a2')

build() {
  gem install --no-user-install --ignore-dependencies -i . ${pkgname#*-}-${pkgver}.gem
}

package() {
  local _rubyver="$(ruby --version | sed 's/.* \(.*\..*\..*\)p.*/\1/')"
  install -dm 755 "${pkgdir}"/usr/lib/ruby/{gems,vendor_ruby}/${_rubyver}
  mv gems/${pkgname#*-}-${pkgver}/lib/* "${pkgdir}"/usr/lib/ruby/vendor_ruby/${_rubyver}/
  mv doc specifications "${pkgdir}"/usr/lib/ruby/gems/${_rubyver}/

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 {gems/${pkgname#*-}-${pkgver},"${pkgdir}"/usr/share/licenses/${pkgname}}/COPYING
}

# vim: ts=2 sw=2 et:
