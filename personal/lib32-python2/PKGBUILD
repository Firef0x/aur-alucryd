# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=lib32-python2
pkgver=2.7.8
pkgrel=1
pkgdesc='A high-level scripting language'
arch=('x86_64')
url='http://www.python.org/'
license=('PSF')
depends=('lib32-bzip2' 'lib32-db' 'lib32-expat' 'lib32-gdbm' 'lib32-libffi'
         'lib32-openssl' 'lib32-sqlite3' 'lib32-zlib' "python2=${pkgver}")
makedepends=('gcc-multilib' 'tk')
optdepends=('tk: for IDLE')
options=('!makeflags')
source=("http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz")
sha256sums=('edde10a0cb7d14e2735e682882d5b287028d1485c456758154c19573db68075a')

prepare() {
  cd Python-${pkgver}

  sed -i "/progname =/s/python/python${pkgver%.*}/" Python/pythonrun.c
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py
  sed -i "s|^#.* /usr/local/bin/python|#!/usr/bin/python2|" Lib/cgi.py
  sed -i "s/python2.3/python2/g" Lib/distutils/tests/test_{build,install}_scripts.py
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*
  touch Include/Python-ast.h Python/Python-ast.c
}

build() {
  cd Python-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  export OPT="${CFLAGS}"

  ./configure --prefix='/usr' --lib{,exec}dir='/usr/lib32' --enable-shared \
              --with-system-{expat,ffi} --with-threads --enable-ipv6 \
              --enable-unicode='ucs4' --with-dbmliborder=gdbm:ndbm

  sed -i -e 's#$(prefix)/lib#$(prefix)/lib32#' Makefile

  make
}

package() {
  cd Python-${pkgver}

  make DESTDIR="${pkgdir}" altinstall
  mv "${pkgdir}"/usr/bin/smtpd.py "${pkgdir}"/usr/lib32/python${pkgver%.*}/
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  ln -sf python-${pkgver%.*}.pc "${pkgdir}"/usr/lib32/pkgconfig/python2.pc
  ln -sf ../../libpython${pkgver%.*}.so "${pkgdir}"/usr/lib32/python${pkgver%.*}/config/

  install -dm 755 "${pkgdir}"/usr/lib32/python${pkgver%.*}/Tools/{i18n,scripts}
  install -m 755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}"/usr/lib32/python${pkgver%.*}/Tools/i18n/
  install -m 755 Tools/scripts/{README,*py} "${pkgdir}"/usr/lib32/python${pkgver%.*}/Tools/scripts/

  find "${pkgdir}"/usr/lib32/python${pkgver%.*}/ -name '*.py' | xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"

  sed -i "s#${srcdir}/Python-${pkgver}:##" "${pkgdir}"/usr/lib32/python${pkgver%.*}/config/Makefile

}

# vim: ts=2 sw=2 et:
