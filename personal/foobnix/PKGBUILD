# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: archtux <antonio.arias99999@gmail.com>

pkgname=foobnix
pkgver=3.0.03
pkgrel=4
pkgdesc='Light and functional music player'
arch=('any')
url='http://www.foobnix.com'
license=('GPL3')
depends=('desktop-file-utils' 'gst-plugins-good' 'libnotify' 'mutagen'
         'python2-chardet' 'python2-gobject' 'python2-simplejson' 'webkitgtk')
makedepends=('git')
optdepends=('fuseiso: ISO support'
            'gst-libav: Support for additional audio formats'
            'gst-plugins-bad: Support for additional audio formats'
            'gst-plugins-ugly: Support for additional audio formats'
            'libkeybinder3: Global hotkeys configuration')
install='foobnix.install'
source=("git+https://github.com/foobnix/foobnix.git#commit=2805cc86f9")
sha256sums=('SKIP')

prepare() {
  cd foobnix

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd foobnix

  python2 setup.py build
}

package() {
  cd foobnix

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
