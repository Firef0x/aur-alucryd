# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jason Ryan <jasonwryan@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowwski.pl>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: evr <evanroman@gmail.com>
# Contributor: Luiz Ribeiro <luizribeiro@gmail.com>

pkgname=mpdscribble
pkgver=0.22
pkgrel=9
pkgdesc='MPD client which submits track info to Last.fm'
url='http://mpd.wikia.com/wiki/Client:Mpdscribble'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmpdclient' 'libsoup')
backup=('etc/mpdscribble.conf')
source=("http://www.musicpd.org/download/mpdscribble/${pkgver}/mpdscribble-${pkgver}.tar.bz2"
        'mpdscribble.service')
sha256sums=('1cde2b5f8c70f0e3e6f059d01bf0b0f2eac2b29fbcd14b01a8a5103e603feb41'
            '275c77ead46ec4295d1fecc20154933b49c3c28922c7657a075b50f5df5ec148')

build() {
  cd mpdscribble-${pkgver}

  ./configure --prefix='/usr' --sysconfdir='/etc' --with-http-client='soup'
  make
}

package() {
  cd mpdscribble-${pkgver}

  make DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}"/etc/mpdscribble.conf

  install -dm 755 "${pkgdir}"/usr/lib/systemd/system
  install -m 644 ../mpdscribble.service "${pkgdir}"/usr/lib/systemd/system/
}

# vim: ts=2 sw=2 et:
