# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: Thomas Wei√üschuh <thomas_weissschuh@lavabit.com>

pkgname=ultrastardx-svn
epoch=1
pkgver=3078
pkgrel=1
pkgdesc='Free and open source karaoke game'
arch=('i686' 'x86_64')
url='http://ultrastardeluxe.org/'
license=('GPL')
depends=('ffmpeg-compat' 'lua' 'portaudio' 'sdl_image' 'sqlite')
makedepends=('subversion' 'fpc')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('ultrastardx::svn+http://svn.code.sf.net/p/ultrastardx/svn/trunk'
        'makefile.patch')
sha256sums=('SKIP'
            '8e20d76ea715d2f20cb5200dc1b5e2c741902cb927b257e56fe72aebcf7f73be')

pkgver() {
  cd ${pkgname%-*}

  svnversion
}

prepare() {
  cd ${pkgname%-*}

  patch -Np1 -i ../makefile.patch
}

build() {
  cd ${pkgname%-*}

  export PKG_CONFIG_PATH='/usr/lib/ffmpeg-compat/pkgconfig'

  ./autogen.sh
  ./configure --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var'
  make LDFLAGS="-O1 --sort-common --as-needed -z relro"
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/{applications,pixmaps}
  install -m 644 dists/${pkgname%-*}.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 "${pkgdir}"/usr/share/${pkgname%-*}/resources/icons/${pkgname%-*}-icon.png \
                 "${pkgdir}"/usr/share/pixmaps/${pkgname%-*}.png
}

# vim: ts=2 sw=2 et:
