# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

pkgname=ruby-mime-types
pkgver=1.25
pkgrel=1
pkgdesc="MIME Content-Type database"
arch=('any')
url="http://mime-types.rubyforge.org"
license=('GPL2' 'MIT' 'PerlArtistic')
depends=('ruby')
source=("http://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem")
noextract=("${pkgname#*-}-${pkgver}.gem")
sha256sums=('ee2dc47f195959f3b0729e889c3dabfce7ba0cf9e04cfba31eddcb02e885a641')

build() {
  gem install --no-user-install --ignore-dependencies -i . ${pkgname#*-}-${pkgver}.gem
}

package() {
  local _rubyver="$(ruby --version | sed 's/.* \(.*\..*\..*\)p.*/\1/')"
  install -dm 755 "${pkgdir}"/usr/lib/ruby/{gems,vendor_ruby}/${_rubyver}
  mv gems/${pkgname#*-}-${pkgver}/lib/* "${pkgdir}"/usr/lib/ruby/vendor_ruby/${_rubyver}/
  mv doc specifications "${pkgdir}"/usr/lib/ruby/gems/${_rubyver}/

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 {gems/${pkgname#*-}-${pkgver},"${pkgdir}"/usr/share/licenses/${pkgname}}/Licence.rdoc
}

# vim: ts=2 sw=2 et:
