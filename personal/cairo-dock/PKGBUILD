# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: erm67 <erm67@yahoo.it>

pkgbase=cairo-dock
pkgname=('cairo-dock' 'cairo-dock-session')
pkgver=3.2.0
pkgrel=1
pkgdesc="Light eye-candy fully themable animated dock"
arch=('i686' 'x86_64')
url="https://launchpad.net/cairo-dock-core"
license=('GPL')
makedepends=('cmake' 'curl' 'dbus-glib' 'gtk3' 'gtkglext' 'inputproto' 'librsvg')
options=('!libtool')
source=("http://launchpad.net/${pkgname}-core/3.2/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz" 'gldit-rpath.patch')
sha256sums=('69d57dab16e70bb0683ba91f1dfd19a9ee0de0b1642ca6480fef9dacbb9c2fd2'
            '5a5fbc67aaa210387ef4410701747fe741942c99c4bd84ae771b96a3bdd1c4cc')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

# Patch
  patch -Np1 -i ../gldit-rpath.patch
  sed -i 's|themes3.2|themes|' CMakeLists.txt

# Build
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCAIRO_DOCK_DISTANT_THEMES_DIR=themes -Denable-desktop-manager=yes
  make
}

package_cairo-dock() {
  depends=('curl' 'dbus-glib' 'gtk3' 'gtkglext' 'librsvg')

  cd "${srcdir}"/${pkgname}-${pkgver}/build

# Install
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin/cairo-dock-session,share/{gnome-session,xsessions}}
}

package_cairo-dock-session() {
  arch=('any')
  depends=('cairo-dock' 'compiz-core' 'gnome-settings-daemon' 'metacity')

  cd "${srcdir}"/${pkgname%-*}-${pkgver}/build

# Install
  make -C data/desktop-manager DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
