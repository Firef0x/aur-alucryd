# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: winlu <winklerlukas@gmail.com>
# Contributor: BlackIkeEagle <ike.devolder@gmail.com>

pkgname=shairplay-git
pkgver=0.r108.c892072
pkgrel=1
pkgdesc='Apple AirPlay and RAOP protocol server'
arch=('i686' 'x86_64')
url='https://github.com/juhovh/shairplay'
license=('GPL3')
depends=('libao')
makedepends=('git')
provides=('libshairport')
conflicts=('libshairport')
options=('!libtool')
source=("git://github.com/juhovh/${pkgname%-*}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-*}

  ./autogen.sh
  ./configure --prefix='/usr'
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
