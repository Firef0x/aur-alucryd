# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=lib32-unixodbc
pkgver=2.3.1
pkgrel=1
pkgdesc='ODBC is an open specification for providing application developers with a predictable API with which to access Data Sources'
arch=('x86_64')
url='http://www.unixodbc.org/'
license=('GPL2' 'LGPL2.1')
depends=('lib32-libltdl' "${pkgname#*-}=${pkgver}")
makedepends=('gcc-multilib')
source=("http://www.unixodbc.org/unixODBC-$pkgver.tar.gz")
sha256sums=('1f5be3edecff9e31072ef738ea1d8019594c4f0c2e3ab427e6eef153491db6a2')

build() {
  cd unixODBC-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --sysconfdir='/etc' --libdir='/usr/lib32'
  make
}

package() {
  cd unixODBC-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share}}
}

# vim: ts=2 sw=2 et:
