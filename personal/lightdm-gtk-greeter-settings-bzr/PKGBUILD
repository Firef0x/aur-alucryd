# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andrew P. <pan.pav.7c5@gmail.com>

pkgname=lightdm-gtk-greeter-settings-bzr
pkgver=r38
pkgrel=1
pkgdesc='Settings editor for LightDM GTK+ Greeter'
arch=('i686' 'x86_64')
url='https://launchpad.net/lightdm-gtk-greeter-settings'
license=('GPL3' 'LGPL3')
depends=('desktop-file-utils' 'gtk3' 'gtk-update-icon-cache' 'python-gobject')
makedepends=('bzr' 'python-distutils-extra')
provides=('lightdm-gtk-greeter-settings')
conflicts=('lightdm-gtk-greeter-settings')
install='lightdm-gtk-greeter-settings.install'
source=('bzr+lp:lightdm-gtk-greeter-settings')
md5sums=('SKIP')

pkgver() {
  cd lightdm-gtk-greeter-settings

  printf "r%s" "$(bzr revno)"
}

package() {
  cd lightdm-gtk-greeter-settings

  python setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:

