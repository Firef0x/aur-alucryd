# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=lib32-gobject-introspection
pkgver=1.34.2
pkgrel=1
pkgdesc="Introspection system for GObject-based libraries"
url="https://live.gnome.org/GObjectIntrospection"
arch=('x86_64' 'i686')
license=('LGPL' 'GPL')
makedepends=('lib32-cairo' 'lib32-mesa-libgl' 'python2' 'python2-mako')
options=('!libtool')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname#*-}/${pkgver::4}/${pkgname#*-}-${pkgver}.tar.xz")
sha256sums=('bc7948a35ce2218922dfb0806679cad21060715cc0c340cf7823eb633cc03429')

build() {
  cd "${pkgname#*-}"-${pkgver}

# Patch
  sed -i 's|#!/usr/bin/env python$|#!/usr/bin/python2|' giscanner/*.py

# Multilib
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

# Build
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static
  make
}

package() {
  depends=("${pkgname#*-}" 'lib32-glib2' 'python2' 'python2-mako')

  cd "${pkgname#*-}"-${pkgver}

# Install
  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
