# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Patrick McCarty <pnorcks@gmail.com>

pkgname=lilypond-devel
pkgver=2.17.95
pkgrel=1
pkgdesc="An automated music engraving system"
arch=('i686' 'x86_64')
url="http://lilypond.org/"
license=('GPL')
depends=('ghostscript' 'guile1.8' 'pango' 'python2')
makedepends=('fontforge' 'gsfonts' 't1utils' 'texlive-core')
provides=("lilypond=${pkgver}")
conflicts=('lilypond')
options=('!emptydirs')
source=("http://download.linuxaudio.org/lilypond/sources/v${pkgver%.*}/lilypond-${pkgver}.tar.gz")
sha256sums=('5f286112624ed3d8c52b6d3776651ad88c2fd6c08b3f1d0049101d37488a3db4')

prepare() {
  cd lilypond-${pkgver}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
  sed -i 's|GUILE_CFLAGS=.*|GUILE_CFLAGS="`pkg-config --cflags guile-1.8`"|
          s|GUILE_LDFLAGS=.*|GUILE_LDFLAGS="`pkg-config --libs guile-1.8`"|' configure
}

build() {
  cd lilypond-${pkgver}

  export GUILE='/usr/bin/guile1.8'
  export GUILE_CONFIG='/usr/bin/guile-config1.8'
  export PYTHON='/usr/bin/python2'
  export PYTHON_CONFIG='python2-config'

  ./configure --prefix=/usr --disable-{debugging,documentation}

  make
}

package() {
  cd lilypond-${pkgver}

  make DESTDIR="${pkgdir}" vimdir='/usr/share/vim/vimfiles' install
}

# vim: ts=2 sw=2 et:
