# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>
# Contributor: Themaister <maister@archlinux.us>

pkgname=retroarch-git
pkgver=1.0.0.3.r888.gf43b222
pkgrel=1
pkgdesc='Reference frontend for the libretro API'
arch=('i686' 'x86_64')
url='http://github.com/libretro/RetroArch'
license=('GPL')
depends=('ffmpeg' 'libxkbcommon' 'nvidia-cg-toolkit' 'openal' 'python' 'sdl2')
makedepends=('git')
provides=('retroarch')
conflicts=('retroarch')
source=("git+https://github.com/libretro/RetroArch.git")
md5sums=('SKIP')

pkgver() {
  cd RetroArch

  printf "%s" "$(git describe | sed 's/-[^-]*//; s/-/.r/; s/-/./')"
}

build() {
  cd RetroArch

  ./configure \
    --prefix='/usr'
  make
}

package() {
  cd RetroArch

  make install DESTDIR="${pkgdir}"
}

# vim: ts=2 sw=2 et:
