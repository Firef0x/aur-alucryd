# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor:Ionut Biru <ibiru@archlinux.org> 

pkgname=lib32-at-spi2-atk
pkgver=2.14.0
pkgrel=1
pkgdesc='A GTK+ module that bridges ATK to D-Bus at-spi'
arch=('x86_64')
url='http://www.gnome.org'
license=('GPL2')
depends=("at-spi2-atk=${pkgver}" 'lib32-at-spi2-core' 'lib32-atk' 'lib32-libx11')
makedepends=('gcc-multilib' 'intltool')
source=("http://download.gnome.org/sources/at-spi2-atk/${pkgver%.*}/at-spi2-atk-${pkgver}.tar.xz")
sha256sums=('56b40ef16d9f1b1630d32addb0cc941372a1e97d8ddafd369f912c7d125688e7')

prepare() {
  cd at-spi2-atk-${pkgver}

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
}

build() {
  cd at-spi2-atk-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  autoreconf -fi
  ./configure --prefix='/usr' --sysconfdir='/etc' --libdir='/usr/lib32' \
              --disable-schemas-compile
  make
}

package() {
  cd at-spi2-atk-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,lib32/gnome-settings-daemon-3.0,share}
}

# vim: ts=2 sw=2 et:
