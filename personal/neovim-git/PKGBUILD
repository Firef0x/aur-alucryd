# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Florian Hahn <flo@fhahn.com>

pkgname=neovim-git
pkgdesc="Vim's rebirth for the 21st century"
pkgver=0.r259.7bd4d68
pkgrel=1
arch=('i686' 'x86_64')
url='https://github.com/neovim/neovim'
license=('custom:vim')
depends=('libuv' 'luajit' 'ncurses')
makedepends=('cmake' 'git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('git+https://github.com/neovim/neovim.git')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd ${pkgname%-*}/build

  install -dm 755 "${pkgdir}"/usr/{bin,share/licenses/${pkgname}}
  install -m 755 bin/nvim "${pkgdir}"/usr/bin/
  install -m 644 ../vim-license.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: sw=2 ts=2 et:
