# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens <jan.steffens@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mladen Pejakovic <pejakm@gmail.com>

pkgbase=lib32-libxkbcommon
pkgname=('lib32-libxkbcommon' 'lib32-libxkbcommon-x11')
pkgver=0.4.3
pkgrel=1
pkgdesc='Keyboard handling library using XKB data'
arch=('x86_64')
url='http://xkbcommon.org/'
license=('custom')
depends=('lib32-glibc' "libxkbcommon=${pkgver}")
makedepends=('gcc-multilib' 'lib32-libxcb' 'xorg-util-macros')
source=("http://xkbcommon.org/download/libxkbcommon-${pkgver}.tar.xz")
sha256sums=('9a52d5d0419e76c49c0ece86208c205ffacb1cf0ff8ffbaba98d3d4dd40c4e41')

prepare() {
  mkdir -p x11/usr/lib32/pkgconfig
}

build() {
  cd ${pkgbase#*-}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --disable-{docs,static}
  make
}

package_lib32-libxkbcommon() {
  cd libxkbcommon-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon

  mv "${pkgdir}"/usr/lib32/*x11* ../x11/usr/lib32
  mv "${pkgdir}"/usr/lib32/pkgconfig/*x11* ../x11/usr/lib32/pkgconfig
}

package_lib32-libxkbcommon-x11() {
pkgdesc='Keyboard handling library using XKB data for X11 XCB clients (32-bit)'
depends=('lib32-libxcb' 'lib32-libxkbcommon' 'libxkbcommon-x11')

  mv x11/* "${pkgdir}"/
  find "${pkgdir}" -type d -exec chmod 755 {} +

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon-x11 "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon-x11
}

# vim: ts=2 sw=2 et:
