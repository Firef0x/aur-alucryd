# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: GordonGR <gordongr@freemail.gr>

pkgname=lib32-polkit
pkgver=0.112
pkgrel=4
pkgdesc='Application development toolkit for controlling system-wide privileges'
arch=('x86_64')
license=('LGPL')
url='http://www.freedesktop.org/wiki/Software/polkit'
depends=('lib32-expat' 'lib32-glib2' 'lib32-js17' 'lib32-pam' 'lib32-systemd' "polkit=${pkgver}")
makedepends=('gcc-multilib' 'gobject-introspection' 'intltool')
source=("http://www.freedesktop.org/software/polkit/releases/polkit-${pkgver}.tar.gz")
sha256sums=('d695f43cba4748a822fbe864dd32c4887c5da1c71694a47693ace5e88fcf6af6')

build() {
  cd polkit-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' \
              --libexecdir='/usr/lib32/polkit-1' --sysconfdir='/etc' \
              --localstatedir=/var --with-mozjs='mozjs-17.0' \
              --with-systemdsystemunitdir='/usr/lib/systemd/system' \
              --disable-{gtk-doc,static} --enable-libsystemd-login='yes' \
              --enable-man-pages='no'
  make
}

package() {
  cd polkit-$pkgver

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,lib,include,share}}
}

# vim: ts=2 sw=2 et:
