# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor:Ionut Biru <ibiru@archlinux.org> 

pkgname=lib32-at-spi2-atk
pkgver=2.15.90
pkgrel=1
pkgdesc='A GTK+ module that bridges ATK to D-Bus at-spi'
arch=('x86_64')
url='https://wiki.gnome.org/Accessibility'
license=('GPL2')
depends=('at-spi2-atk' 'lib32-at-spi2-core' 'lib32-atk')
makedepends=('gcc-multilib' 'intltool')
source=("http://download.gnome.org/sources/at-spi2-atk/${pkgver%.*}/at-spi2-atk-${pkgver}.tar.xz")
sha256sums=('997c1ff85b7731d4c852ebef20f7bd65f20340823ca6ce1134fbd2efaa234b1e')

prepare() {
  cd at-spi2-atk-${pkgver}

  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf -fi
}

build() {
  cd at-spi2-atk-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --sysconfdir='/etc' \
    --disable-schemas-compile
  make
}

package() {
  cd at-spi2-atk-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,lib32/gnome-settings-daemon-3.0,share}
}

# vim: ts=2 sw=2 et:
