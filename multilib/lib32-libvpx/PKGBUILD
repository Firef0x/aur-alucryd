# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-libvpx
pkgver=1.3.0
pkgrel=1
pkgdesc='The VP8 Codec SDK'
arch=('x86_64')
url='http://www.webmproject.org/'
license=('BSD')
depends=('lib32-glibc' 'libvpx')
makedepends=('yasm')
source=("https://webm.googlecode.com/files/libvpx-v${pkgver}.tar.bz2")
sha256sums=('d3dcc8d84af51c6c382b214397c62402e37a799e8ebcda6f4217aef0010451a9')

build() {
  cd libvpx-v${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --target='x86-linux-gcc' \
    --enable-pic \
    --enable-postproc \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-vp{8,9} \
    --disable-install-{bins,docs,srcs}
  make
}

package() {
  cd libvpx-v${pkgver}

  make DIST_DIR="${pkgdir}/usr" install
  rm -rf "${pkgdir}"/usr/include

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libvpx "${pkgdir}"/usr/share/licenses/lib32-libvpx
}

# vim: ts=2 sw=2 et:
