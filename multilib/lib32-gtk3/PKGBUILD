# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>

pkgname=lib32-gtk3
pkgver=3.14.0
pkgrel=1
pkgdesc='GObject-based multi-platform GUI toolkit (v3)'
arch=('x86_64')
license=('LGPL')
url='http://www.gtk.org/'
depends=("gtk3=${pkgver}" 'lib32-at-spi2-atk' 'lib32-atk' 'lib32-cairo'
         'lib32-colord' 'lib32-libcups' 'lib32-fontconfig' 'lib32-gdk-pixbuf2'
         'lib32-glib2' 'lib32-json-glib' 'lib32-pango' 'lib32-rest'
         'lib32-libxcomposite' 'lib32-libxcursor' 'lib32-libxdamage'
         'lib32-libxi' 'lib32-libxinerama' 'lib32-libxkbcommon'
         'lib32-libxrandr' 'lib32-wayland')
makedepends=('gcc-multilib' 'gobject-introspection')
install='gtk3.install'
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver:0:4}/gtk+-$pkgver.tar.xz")
sha256sums=('68d6b57d15c16808d0045e96b303f3dd439cc22a9c06fdffb07025cd713a82bc')

build() {
  cd gtk+-${pkgver}

  export CC='gcc -m32'
  export CXX='/bin/false'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --sysconfdir='/etc' \
              --localstatedir='/var' --enable-gtk2-dependency \
              --enable-{broadway,wayland,x11}-backend --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd gtk+-${pkgver}

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
  rm "${pkgdir}"/usr/bin/{broadwayd,gtk-{encode-symbolic-svg,launch},gtk3-{demo,demo-application,icon-browser,widget-factory}}
  rm -rf "${pkgdir}"/{etc,usr/{include,share}}
}

# vim: ts=2 sw=2 et:
