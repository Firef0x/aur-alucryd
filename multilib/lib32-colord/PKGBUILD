# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-colord
pkgver=1.2.3
pkgrel=2
pkgdesc='System daemon for managing color devices'
arch=('x86_64')
url='http://www.freedesktop.org/software/colord'
license=('GPL2')
depends=("colord=${pkgver}" 'lib32-dconf' 'lib32-libgusb' 'lib32-lcms2'
         'lib32-polkit' 'lib32-sqlite')
makedepends=('gcc-multilib' 'gobject-introspection' 'intltool' 'python2'
             'vala')
source=("http://www.freedesktop.org/software/colord/releases/colord-${pkgver}.tar.xz")
sha256sums=('2091fcca19d049136644e9dc6d56849b0e29d441a81a955d8f0e8135163474fc')

build() {
  cd colord-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32/colord' \
    --localstatedir='/var' \
    --sysconfdir='/etc' \
    --enable-vala \
    --disable-bash-completion \
    --disable-static \
    --disable-volume-search \
    --with-daemon-user='colord' \
    --with-systemdsystemunitdir='/usr/lib/systemd/system'
  make
}

package() {
  cd colord-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,var,usr/{bin,include,lib,share}}
}

# vim: ts=2 sw=2 et:
