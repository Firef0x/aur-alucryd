# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=lib32-libva
pkgver=1.4.1
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('lib32-libdrm' 'lib32-libgl' 'lib32-libxfixes' 'libva')
makedepends=('gcc-multilib' 'mesa')
optdepends=('lib32-libva-vdpau-driver: backend for Nvidia and AMD cards'
            'lib32-libva-intel-driver: backend for Intel cards')
source=("http://freedesktop.org/software/vaapi/releases/libva/libva-${pkgver}.tar.bz2")
sha256sums=('0662af0ecdf1cfd2bb47e86f5bf66009554fe12ce7bf5b9cec6a2cc43f3f4c11')

build() {
  cd libva-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32'
  make
}

package() {
  cd libva-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libva "${pkgdir}"/usr/share/licenses/lib32-libva
}

# vim: ts=2 sw=2 et:
