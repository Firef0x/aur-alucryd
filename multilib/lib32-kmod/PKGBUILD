# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Jesse Jaara <jesse.jaara@gmail.com>

pkgname=lib32-kmod
pkgver=18
pkgrel=1
pkgdesc='Linux kernel module handling'
arch=('x86_64')
url='http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary'
license=('GPL2')
depends=("kmod=${pkgver}" 'lib32-zlib')
makedepends=('gcc-multilib')
source=("https://www.kernel.org/pub/linux/utils/kernel/kmod/kmod-${pkgver}.tar.xz")
md5sums=('82835c7f01983634e06ca72b4ee30cc6')

build() {
  cd kmod-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --sysconfdir='/etc' --libdir='/usr/lib32' --with-zlib
  make
}

package() {
  cd kmod-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
