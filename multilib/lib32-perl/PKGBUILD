# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: kevin <kevin.archlinux.org>
# Contributor: judd <jvinet.zeroflux.org>
# Contributor: francois <francois.archlinux.org>

pkgname=lib32-perl
pkgver=5.20.1
pkgrel=1
pkgdesc='A highly capable, feature-rich programming language'
arch=('x86_64')
url='http://www.perl.org'
license=('GPL' 'PerlArtistic')
depends=('lib32-db' 'lib32-gdbm' 'lib32-glibc')
makedepends=('gcc-multilib')
source=("http://www.cpan.org/src/5.0/perl-${pkgver}.tar.bz2")
options=('makeflags' '!purge' 'emptydirs')
sha256sums=('ede5ded37e7fb6139b04728cfca826f17076f9888dbfd100a56834dbeb04657c')

build() {
  cd perl-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./Configure -des -Dusethreads -Duseshrplib -Doptimize="${CFLAGS}" \
              -Dprefix='/usr' -Dvendorprefix='/usr' \
              -Dprivlib='/usr/share/perl5/core_perl' \
              -Darchlib='/usr/lib32/perl5/core_perl' \
              -Dsitelib='/usr/share/perl5/site_perl' \
              -Dsitearch='/usr/lib32/perl5/site_perl' \
              -Dvendorlib='/usr/share/perl5/vendor_perl' \
              -Dvendorarch='/usr/lib32/perl5/vendor_perl' \
              -Dscriptdir='/usr/bin/core_perl' \
              -Dsitescript='/usr/bin/site_perl' \
              -Dvendorscript='/usr/bin/vendor_perl' \
              -Dinc_version_list='none' \
              -Dman1ext='1perl' -Dman3ext='3perl' \
              -Dlddlflags="-shared ${LDFLAGS}" -Dldflags="${LDFLAGS}"
  make
}

package() {
  cd perl-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  sed -e '/^man1ext=/ s/1perl/1p/' -e '/^man3ext=/ s/3perl/3pm/' \
      -e "/^cf_email=/ s/'.*'/''/" \
      -e "/^perladmin=/ s/'.*'/''/" \
      -i "${pkgdir}"/usr/lib32/perl5/core_perl/Config_heavy.pl

  find "${pkgdir}"/usr/lib32 -name *.pod -delete
  find "${pkgdir}" -name .packlist -delete
  find "${pkgdir}" -type d -exec chmod 755 {} +
}

#vim: ts=2 sw=2 et:
