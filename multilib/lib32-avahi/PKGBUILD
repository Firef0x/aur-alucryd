# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gicu Gorodenco <cyclopsihus@gmail.com>

pkgname=lib32-avahi
pkgver=0.6.31
pkgrel=9
pkgdesc='A multicast/unicast DNS-SD framework'
arch=('x86_64')
url='http://www.avahi.org'
license=('LGPL')
depends=('avahi' 'lib32-dbus-core' 'lib32-gdbm' 'lib32-glib2' 'lib32-libcap'
         'lib32-libdaemon')
makedepends=('gcc-multilib' 'gobject-introspection' 'intltool' 'python2-dbus')
source=("http://www.avahi.org/download/avahi-${pkgver}.tar.gz")
sha256sums=('8372719b24e2dd75de6f59bb1315e600db4fd092805bd1201ed0cb651a2dab48')

prepare() {
  cd avahi-${pkgver}

  sed '/^Libs:/s:$: -ldbus-1:' -i avahi-client.pc.in
  sed 's:netdev:network:g' -i avahi-daemon/avahi-dbus.conf
  sed 's:/sbin/resolvconf:/usr/sbin/resolvconf:g' -i */*.action
  sed 's:-DG[^ ]*_DISABLE_DEPRECATED=1::g' -i avahi-ui/Makefile.*
}

build() {
  cd avahi-${pkgver}

  export PYTHON='/usr/bin/python2'
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --localstatedir='/var' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --enable-compat-{howl,libdns_sd} \
    --disable-{gtk,gtk3,mono,monodoc,pygtk,qt3,qt4} \
    --with-autoipd-{group,user}='avahi' \
    --with-avahi-priv-access-group='network' \
    --with-systemdsystemunitdir='/usr/lib/systemd/system' \
    --with-distro='archlinux'
  make
}

package() {
  cd avahi-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,lib,share},var}
}

# vim: ts=2 sw=2 et:
