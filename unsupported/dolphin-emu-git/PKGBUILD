# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Lightning <sgsdxzy@gmail.com>

pkgname=dolphin-emu-git
pkgver=4.0.r3013.97420c6
pkgrel=1
pkgdesc='A GameCube / Wii / Triforce emulator'
arch=('x86_64')
url='http://www.dolphin-emu.org/'
license=('GPL2')
depends=('bluez-libs' 'ffmpeg' 'libao' 'libusb' 'miniupnpc' 'openal' 'polarssl' 'portaudio' 'sdl2' 'soundtouch' 'wxgtk')
makedepends=('cmake' 'git')
optdepends=('pulseaudio: PulseAudio backend')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!emptydirs')
source=("${pkgname%-*}::git+https://github.com/dolphin-emu/dolphin.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "%s" "$(git describe --tags | sed 's/-/.r/; s/-g/./')"
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_CXX_FLAGS='-fno-inline-functions' \
           -DENABLE_LTO='TRUE'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
  install -m 755 Binaries/dolphin-emu-nogui "${pkgdir}"/usr/bin/
}

# vim: ts=2 sw=2 et:
