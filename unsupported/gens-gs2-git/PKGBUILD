# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=gens-gs2-git
pkgver=20130321
pkgrel=1
pkgdesc="A Sega Genesis / Sega CD / Sega 32X emulator"
arch=('i686' 'x86_64')
url="http://segaretro.org/Gens/GS_II"
license=('GPL')
if [[ $CARCH == "x86_64" ]]; then
  depends=('lib32-glew' 'lib32-mesa' 'lib32-portaudio' 'lib32-qt4')
  makedepends=('cmake' 'gcc-multilib' 'git' 'nasm')
  optdepends=('lib32-alsa-plugins: Sound support for PulseAudio'
              'lib32-libpulse: Sound support for PulseAudio')
else
  depends=('glew' 'mesa' 'portaudio' 'qt4')
  makedepends=('cmake' 'git' 'nasm')
fi
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('gens-qt4.desktop'
        'gens-qt4.png')
sha256sums=('a501f5f1fe478089de0be5904d09353ae3921241243708599a44288083162a88'
            'bb54a11ed24ea2bed912078faf027c3eeba7b1fe33a17496a834664d821b6bed')

_gitroot=git://dusers.drexel.edu/srv/git/~korth/gens-gs-ii.git
_gitname=gens-gs-ii

build() {
  cd "${srcdir}"

# Clone
  msg "Connecting to GIT server...."

  if [[ -d ${_gitname} ]]; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_gitname}-build
  git clone "${srcdir}"/${_gitname} "${srcdir}"/${_gitname}-build
  cd "${srcdir}"/${_gitname}-build

# Multilib
  if [[ $CARCH == "x86_64" ]]; then
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  fi

# Build
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/${_gitname}-build/build

# Install
  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,pixmaps}}
  install -m 755 src/gens-qt4/gens-qt4 "${pkgdir}"/usr/bin/gens-qt4
  install -m 644 "${srcdir}"/gens-qt4.desktop "${pkgdir}"/usr/share/applications/gens-qt4.desktop
  install -m 644 "${srcdir}"/gens-qt4.png "${pkgdir}"/usr/share/pixmaps/gens-qt4.png
}

# vim: ts=2 sw=2 et:
