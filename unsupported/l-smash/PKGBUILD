# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=l-smash
pkgver=1.13.27
pkgrel=1
pkgdesc='A simple tool for MP4'
arch=('i686' 'x86_64')
url='https://github.com/l-smash/l-smash'
license=('custom')
depends=('glibc')
source=("l-smash-${pkgver}.tar.gz::https://github.com/l-smash/l-smash/archive/v${pkgver}.tar.gz")
sha256sums=('0d1070532196bfebd7b9793ead27b544e60d8af98d94e76ef072dd97738910be')

build() {
  cd l-smash-${pkgver}

  ./configure \
    --prefix='/usr' \
    --enable-shared \
    --disable-static
  make
}

package() {
  cd l-smash-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
