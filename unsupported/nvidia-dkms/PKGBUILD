# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: jarda-wien <xstej70 at gmail dot com>
# Contributor: Jason Melton <jason dot melton at gmail dot com>
# Contributor: Atilla ÖNTAŞ <tarakbumba at gmail dot com>

pkgname=nvidia-dkms
pkgver=319.32
pkgrel=4
pkgdesc="NVIDIA kernel module sources (DKMS)"
arch=('i686' 'x86_64')
[[ $CARCH == i686 ]] && _arch=x86 && _pkg=NVIDIA-Linux-${_arch}-${pkgver} && md5sums=('528fc538584de6446384b9cec3099d6c')
[[ $CARCH == x86_64 ]] && _arch=x86_64 && _pkg=NVIDIA-Linux-${_arch}-${pkgver}-no-compat32 && md5sums=('63b2caa0cb128efa1a7808d7bddb2074')
url="http://www.nvidia.com/"
license=('custom')
depends=('dkms' "nvidia-utils=${pkgver}")
optdepends=('linux-headers: build the module against Arch kernel'
            'linux-lts-headers: build the module against LTS Arch kernel')
provides=("nvidia=${pkgver}")
conflicts=('nvidia')
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
        'linux310.patch')
md5sums+=('c61091da43ee311335e87d21b469cb83')
install=${pkgname}.install

prepare() {
  cd "${srcdir}"

  if [[ -d ${_pkg} ]] ; then
    rm -rf ${_pkg}
  fi

  sh ${_pkg}.run --extract-only

  cd ${_pkg}
  patch -Np1 -i ../linux310.patch
}

package() {
  cd "${srcdir}"/${_pkg}/kernel

  install -dm 755 "${pkgdir}"/usr/{lib/modprobe.d,share/licenses,src/nvidia-${pkgver}}
  cp -dr --no-preserve=ownership * "${pkgdir}"/usr/src/nvidia-${pkgver}/
  echo "blacklist nouveau" > "${pkgdir}"/usr/lib/modprobe.d/nvidia.conf
  ln -s /usr/share/licenses/nvidia "${pkgdir}"/usr/share/licenses/${pkgname}

# Fix permissions
  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
  chmod 755 "${pkgdir}"/usr/src/nvidia-${pkgver}/conftest.sh
}

# vim: ts=2 sw=2 et:
md5sums=('d181d856768868823df3bda4607a5315'
         '221cafe264f5f4d76e543781b07ac954')
