# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: L42y <423300@gmail.com>
# Contributor: teddy_beer_maniac <teddy_beer_maniac@wp.pl>

pkgname=gnome-common-git
pkgver=3.10.0.r3.62f7546
pkgrel=1
pkgdesc="Common development macros for GNOME"
arch=('any')
url="http://www.gnome.org"
license=('GPL')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("git://git.gnome.org/${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "%s" "$(git describe --tags | sed 's/-/.r/; s/-g/./')"
}

build() {
  cd ${pkgname%-*}

  ./autogen.sh --prefix='/usr'
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
