# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Sebastian Lenz <sebastian@archusers.de>

_ext=user-theme
pkgname=gnome-shell-extension-${_ext}-git
pkgver=20130311
pkgrel=1
pkgdesc="Load shell themes from user directory"
arch=('any')
url="http://live.gnome.org/GnomeShell/Extensions"
license=('GPL' 'LGPL')
depends=('gnome-shell-extension-common-git')
makedepends=('git' 'gnome-common-git' 'intltool')
provides=("gnome-shell-extension-${_ext}")
conflicts=('gnome-shell-extensions-git' "gnome-shell-extension-${_ext}")
install=${pkgname}.install

_gitroot=http://git.gnome.org/browse/gnome-shell-extensions
_gitname=gnome-shell-extensions

build() {
  cd "${srcdir}"

# Clone
  msg "Connecting to GIT server...."

  if [[ -d ${_gitname} ]]; then
    cd ${_gitname} && git pull origin && git checkout gnome-3-6
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
    cd ${_gitname} && git checkout gnome-3-6
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_gitname}-build
  git clone "${srcdir}"/${_gitname} "${srcdir}"/${_gitname}-build
  cd "${srcdir}"/${_gitname}-build

# Build
  ./autogen.sh --prefix=/usr --disable-schemas-compile --enable-extensions=${_ext}
  make
}

package() {
  cd "${srcdir}"/${_gitname}-build/extensions/${_ext}

# Install
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
