# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=aegisub-git
pkgver=3.0.4.r6738.3d573f0
pkgrel=1
pkgdesc="A general-purpose subtitle editor with ASS/SSA support"
arch=('i686' 'x86_64')
url="http://www.aegisub.org"
license=('GPL' 'BSD')
depends=('boost-libs' 'desktop-file-utils' 'ffms2' 'fftw' 'hunspell' 'lua51' 'wxgtk2.9')
makedepends=('boost' 'git' 'intltool' 'mesa')
optdepends=('libpulse: Pulseaudio backend'
            'openal: OpenAL backend'
            'oss: OSS backend'
            'portaudio: Portaudio backend')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("${pkgname%-*}::git+https://github.com/Aegisub/Aegisub.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "3.0.4.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build () {
  cd ${pkgname%-*}/${pkgname%-*}

  ./autogen.sh
  ./configure --prefix='/usr' --with-wxdir='/usr/include/wx-2.9' --with-wx-config='/usr/bin/wx-config-2.9'
  make
}

package() {
  cd ${pkgname%-*}/${pkgname%-*}

  make DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/aegisub.svg

  install -dm 755 "${pkgdir}"/usr/share/licenses/aegisub-git
  install -m 644 {,"${pkgdir}"/usr/share/licenses/aegisub-git/}LICENCE
}

# vim: ts=2 sw=2 et:
