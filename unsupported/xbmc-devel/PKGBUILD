# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=xbmc-devel
pkgver=13.0_beta2
pkgrel=2
pkgdesc='XBMC Media Center - Development snapshots'
arch=('i686' 'x86_64')
url='http://xbmc.org/'
license=('GPL2')
depends=('avahi' 'bluez-libs' 'curl' 'hicolor-icon-theme' 'java-runtime' 'lame' 'libass' 'libbluray' 'libcdio' 'libmicrohttpd' 'libmad' 'libmariadbclient' 'libmodplug' 'libmpeg2' 'libpulse' 'libsamplerate' 'libssh' 'libxrandr' 'libxslt' 'lzo2' 'mesa' 'mesa-demos' 'python2' 'rtmpdump' 'sdl_image' 'taglib' 'tinyxml' 'xorg-xdpyinfo' 'yajl')
makedepends=('afpfs-ng' 'boost' 'cmake' 'doxygen' 'git' 'gperf' 'jasper' 'java-environment' 'libcec' 'libnfs' 'libplist' 'libshairport' 'nasm' 'smbclient' 'swig' 'unzip' 'zip')
optdepends=('afpfs-ng: Apple shares support'
            'smbclient: Windows shares support'
            'libnfs: NFS shares support'
            'libplist: AirPlay support'
            'libshairport: AirPlay support'
            'libcec: Pulse-Eight USB-CEC adapter support'
            'pulseaudio: PulseAudio support')
provides=("${pkgname%-}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=("https://github.com/xbmc/xbmc/archive/Gotham_${pkgver#*_}.tar.gz"
        'xbmc.service')
sha256sums=('c15ff981498ef003507bb542667a7b1bc0d24e9d6182900d14734b8dbff8acd6'
            '0713c3df26656ac2065c411c3d08f6d78515eff76dafec5c496cd11960451642')

build() {
  cd xbmc-Gotham_${pkgver#*_}

  export PYTHON_VERSION='2'

  ./bootstrap
  ./configure --prefix='/usr' --exec-prefix='/usr' --enable-{external-libraries,libbluray,pulse} --disable-{debug,non-free,static}
  make
}

package() {
  cd xbmc-Gotham_${pkgver#*_}

  make DESTDIR="${pkgdir}" install

# Python2
  sed 's/python/python2/g' -i "${pkgdir}"/usr/{bin/xbmc,share/xbmc/addons/service.xbmc.versioncheck/service.py}

# Texture Packer
  install -m 755 {tools/TexturePacker,"${pkgdir}"/usr/lib/xbmc}/TexturePacker

# Systemd
  install -dm 755 "${pkgdir}"/usr/lib/systemd/system
  install -m 644 {..,"${pkgdir}"/usr/lib/systemd/system}/xbmc.service
}

# vim: ts=2 sw=2 et:
