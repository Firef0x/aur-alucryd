# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary-devel
pkgver=0.7.2
pkgrel=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=('i686' 'x86_64')
url='https://wiki.gnome.org/Apps/Geary'
license=('GPL3')
depends=('gnome-keyring' 'gmime' 'libcanberra' 'libgee' 'libnotify' 'webkitgtk')
makedepends=('cmake' 'gnome-doc-utils' 'gobject-introspection' 'intltool' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=("https://download.gnome.org/sources/${pkgname%-*}/${pkgver%.?}/${pkgname%-*}-${pkgver}.tar.xz")
sha256sums=('a121c808d41a735b1d36c04bca742014a35baec423ebcfb97e43fd7bd9767b30')

build() {
  cd ${pkgname%-*}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' \
           -D{DESKTOP_UPDATE,GSETTINGS_COMPILE{,_IN_PLACE},ICON_UPDATE}='FALSE'
  make
}

package() {
  cd ${pkgname%-*}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
