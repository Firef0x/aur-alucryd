# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgbase=vbam-svn
pkgname=('vbam-cli-svn' 'vbam-gtk-svn' 'vbam-wx-svn')
pkgver=1.8.0.r1231
pkgrel=1
arch=('i686' 'x86_64')
url='http://vba-m.com'
license=('GPL2')
makedepends=('cairo' 'chrpath' 'cmake' 'desktop-file-utils' 'ffmpeg-compat'
             'gtkglextmm' 'imagemagick' 'openal' 'subversion' 'wxgtk2.8' 'zip')
[[ $CARCH == i686 ]] && makedepends+=('nasm')
source=("vbam::svn://svn.code.sf.net/p/vbam/code/trunk"
        'http://downloads.sourceforge.net/sfml/SFML-1.6-sdk-linux-64.tar.gz'
        'http://www.lonesock.net/files/soil.zip'
        'sfml.patch'
        'soil.patch')
sha256sums=('SKIP'
            '05bdc32286ab2ec8cb1ccdafe53f31830284e09e11ebfc3ab3836c99a0d9654b'
            'a2305b8d64f6d636e36d669bbdb0ca5445d1345c754b3d61d3f037dad2e5f701'
            'c3c2fc48f4edc303ad24d91583a57aef9b7ca3b78de1d6734cb666745fff9bad'
            '65705aeef90667bd4e28dd03ac0b43685470f2925b642637b7ff6e2f3acd34c7')

pkgver() {
  cd vbam

  printf "1.8.0.r%s" "$(svnversion)"
}

prepare() {
  patch -Np0 -i soil.patch
  patch -Np0 -i sfml.patch

  cd vbam

  sed 's/wxrc/wxrc-2.8/g' -i {po/update_pot.sh,src/wx/CMakeLists.txt}
  sed 's/fn, fnbuf/fn/' -i src/wx/panel.cpp
}

build() {
  pushd 'Simple OpenGL Image Library/src'
  make -f "../projects/makefile/alternate Makefile.txt"
  make -f "../projects/makefile/alternate Makefile.txt" DESTDIR="${srcdir}/staging" install
  popd

  pushd SFML-1.6
  make
  make DESTDIR="${srcdir}/staging/usr" install
  popd

  cd vbam

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  export PKG_CONFIG_PATH='/usr/lib/ffmpeg-compat/pkgconfig'
  local _cmakeargs="-DCMAKE_INSTALL_PREFIX='/usr' \
                    -DCMAKE_CXX_FLAGS=-I${srcdir}/staging/usr/include \
                    -DCMAKE_EXE_LINKER_FLAGS='-L/usr/lib/ffmpeg-compat' \
                    -DENABLE_FFMPEG='TRUE' -DENABLE_LINK='TRUE' \
                    -DENABLE_GTK='TRUE' -DENABLE_WX='TRUE' \
                    -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8' \
                    -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8'
                    -DSFML_INCLUDE_DIR=${srcdir}/staging/usr/include/SFML \
                    -DSFML_AUDIO_INCLUDE_DIR=${srcdir}/staging/usr/include/SFML/Audio \
                    -DSFML_AUDIO_LIBRARY=${srcdir}/staging/usr/lib/libsfml-audio-s.a \
                    -DSFML_GRAPHICS_INCLUDE_DIR=${srcdir}/staging/usr/include/SFML/Graphics \
                    -DSFML_GRAPHICS_LIBRARY=${srcdir}/staging/usr/lib/libsfml-graphics-s.a \
                    -DSFML_NETWORK_INCLUDE_DIR=${srcdir}/staging/usr/include/SFML/Network \
                    -DSFML_NETWORK_LIBRARY=${srcdir}/staging/usr/lib/libsfml-network-s.a \
                    -DSFML_SYSTEM_INCLUDE_DIR=${srcdir}/staging/usr/include/SFML/System \
                    -DSFML_SYSTEM_LIBRARY=${srcdir}/staging/usr/lib/libsfml-system-s.a"

  if [[ $CARCH == i686 ]]; then
    cmake .. ${_cmakeargs} -DENABLE_ASM_{CORE,SCALERS}='TRUE'
  else
    cmake .. ${_cmakeargs}
  fi
  make
}

package_vbam-cli-svn() {
pkgdesc="Nintendo GameBoy Advance emulator - CLI version"
depends=('glu' 'libpng' 'libsm' 'mesa' 'sdl')
conflicts=('vbam-cli')
backup=('etc/vbam.cfg')

  cd vbam/build

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin/{g,wx}vbam,share}
}

package_vbam-gtk-svn() {
pkgdesc="Nintendo GameBoy Advance emulator - GTK+ version"
depends=('desktop-file-utils' 'gtkglextmm' 'sdl')
conflicts=('vbam-gtk' 'vbam-wx' 'vbam-wx-svn')
install='vbam-gtk.install'

  cd vbam/build

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/bin/vbam}
  find "${pkgdir}" -type f -regex .*wxvbam.* -exec rm {} +
}

package_vbam-wx-svn() {
pkgdesc="Nintendo GameBoy Advance emulator - wxWidgets version"
depends=('glu' 'ffmpeg-compat' 'wxgtk2.8')
conflicts=('vbam-gtk' 'vbam-gtk-svn' 'vbam-wx')
options=('!emptydirs')
install='vbam-wx.install'

  cd vbam/build

  make DESTDIR="${pkgdir}" install
  chrpath -d "${pkgdir}"/usr/bin/wxvbam
  rm -rf "${pkgdir}"/{etc,usr/{bin,share}/vbam}
  find "${pkgdir}" -type f -regex .*gvbam.* -exec rm {} +
}

# vim: ts=2 sw=2 et:
