# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=lutris-git
pkgver=0.3.2.r52.af5d252
pkgrel=1
pkgdesc='Open Gaming Platform'
arch=('any')
url='http://lutris.net/'
license=('GPL3')
depends=('dconf' 'desktop-file-utils' 'hicolor-icon-theme' 'polkit' 'python2-gobject' 'python2-xdg' 'python2-yaml')
makedepends=('git' 'python2-setuptools')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=("git+https://github.com/lutris/lutris.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "%s" "$(git describe | sed 's/v//; s/-/.r/; s/-g/./')"
}

prepare() {
  cd ${pkgname%-*}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

package() {
  cd ${pkgname%-*}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
