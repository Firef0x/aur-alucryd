# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=nvidia-304xx-dkms
pkgver=304.119
pkgrel=1
pkgdesc='NVIDIA kernel module sources (DKMS)'
arch=('i686' 'x86_64')
[[ $CARCH == i686 ]] && _arch=x86 && _pkg=NVIDIA-Linux-${_arch}-${pkgver} && md5sums=('a2acb8066ac296005dbf560ee83ae6d9')
[[ $CARCH == x86_64 ]] && _arch=x86_64 && _pkg=NVIDIA-Linux-${_arch}-${pkgver}-no-compat32 && md5sums=('fb93002115c99f15b6ca393a90a9c9ec')
url='http://www.nvidia.com/'
license=('custom')
depends=('bash' 'dkms' "nvidia-304xx-utils=${pkgver}")
optdepends=('linux-headers: build the module for Arch kernel'
            'linux-lts-headers: build the module for LTS Arch kernel')
provides=("nvidia=${pkgver}" 'nvidia-dkms')
conflicts=('nvidia-304xx' 'nvidia-dkms')
install="${pkgname}.install"
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
        "${pkgname%-*}-linux3.13.patch")
md5sums+=('6e7eb8cb6d7b472cdb2ecbc45d8d0d72')

prepare() {
  if [[ -d ${_pkg} ]] ; then
    rm -rf ${_pkg}
  fi

  sh ${_pkg}.run --extract-only

  cd ${_pkg}
  patch -Np1 -i ../${pkgname%-*}-linux3.13.patch
}

package() {
  cd ${_pkg}

  install -dm 755 "${pkgdir}"/usr/{lib/modprobe.d,share/licenses,src}
  cp -dr --no-preserve=ownership kernel "${pkgdir}"/usr/src/nvidia-${pkgver}
  echo 'blacklist nouveau' > "${pkgdir}"/usr/lib/modprobe.d/nvidia.conf
  ln -s nvidia-304xx "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
