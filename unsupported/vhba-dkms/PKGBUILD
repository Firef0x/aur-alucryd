# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Gustavo Alvarez <sl1pkn07 at gmail dot com>

pkgname=vhba-dkms
pkgver=20120422
pkgrel=2
pkgdesc="VHBA kernel module sources (DKMS)"
arch=('any')
url="http://cdemu.sourceforge.net/"
license=('GPL2')
depends=('dkms')
provides=("vhba-module=${pkgver}")
conflicts=('vhba-module')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/cdemu/vhba-module-${pkgver}.tar.bz2" 'dkms.conf' '60-vhba.rules')
sha256sums=('23d968aa841af25579238bb75d0f8a8e73c2562d7205d22a5af63ef681e53f81'
            'a663db5183d0c7e2d830d33d56aeaaeded2f3d13d72f602bca6c74549c7696ae'
            '554bc9a4c609c77c359a609689cf33e36257bb06d429badb30e5be81640034f1')

package() {
  cd "${srcdir}"/vhba-module-${pkgver}

# Install
  install -dm 755 "${pkgdir}"/usr/{lib/udev/rules.d,src/vhba-${pkgver}}
  install -m 644 vhba.c "${pkgdir}"/usr/src/vhba-${pkgver}/vhba.c
  install -m 644 Makefile "${pkgdir}"/usr/src/vhba-${pkgver}/Makefile
  install -m 644 "${srcdir}"/dkms.conf "${pkgdir}"/usr/src/vhba-${pkgver}/dkms.conf
  install -m 644 "${srcdir}"/60-vhba.rules "${pkgdir}"/usr/lib/udev/rules.d/60-vhba.rules
  sed -i "s|@VERSION@|${pkgver}|g" "${pkgdir}"/usr/src/vhba-${pkgver}/dkms.conf
}

# vim: ts=2 sw=2 et:
