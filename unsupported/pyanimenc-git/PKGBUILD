# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=pyanimenc-git
pkgver=r25.e0099b7
pkgrel=1
pkgdesc='Python Transcoding Tools'
arch=('any')
url='https://github.com/alucryd/pyanimenc'
license=('GPL3')
depends=('ffms2' 'python-gobject' 'python-lxml' 'vapoursynth')
optdepends=('fdkaac: fdkaac encoder'
            'lame: lame encoder'
            'vapoursynth-plugin-flash3kyuu_deband-git: f3kdb deband filter'
            'vapoursynth-plugin-fluxsmooth-git: FluxSmooth denoise filters'
            'vapoursynth-plugin-temporalsoften-git: TemporalSoften denoise filter'
            'x264: x264 and x264-10bit encoders'
            'x265: x265 encoder')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package(){
  cd ${pkgname%-*}

  install -dm 755 "${pkgdir}"/usr/{bin,lib/python3.4/site-packages/${pkgname%-*},share/${pkgname%-*}}
  install -m 755 pyanimenc "${pkgdir}"/usr/bin/
  install -m 755 {chapter-editor,vs-script-creator}.py "${pkgdir}"/usr/lib/python3.4/site-packages/${pkgname%-*}/
  install -m 644 pyanimenc.py "${pkgdir}"/usr/lib/python3.4/site-packages/
  install -m 644 *.glade "${pkgdir}"/usr/share/${pkgname%-*}/
  install -m 644 chapters.yml.example "${pkgdir}"/usr/share/${pkgname%-*}/chapters.yml

  cd "${pkgdir}"/usr/lib/python3.4/site-packages

  python -O -m compileall *.py ${pkgname%-*}/*.py
}

# vim: ts=2 sw=2 et:
