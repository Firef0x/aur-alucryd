# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=gnome-shell-extension-mediaplayer-git
pkgver=20130403
pkgrel=1
pkgdesc="A mediaplayer indicator for the Gnome Shell"
arch=('any')
url="https://github.com/eonpatapon/gnome-shell-extensions-mediaplayer"
license=('GPL2')
depends=('gnome-shell')
makedepends=('git' 'gnome-common' 'intltool')
optdepends=('mpdris2-git: MPD support')
install=${pkgname%-*}.install
source=("${pkgname%-*}::git+https://github.com/eonpatapon/gnome-shell-extensions-mediaplayer.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  ./autogen.sh
  ./configure --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
