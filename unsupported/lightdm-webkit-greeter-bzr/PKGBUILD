# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Christian Hesse <mail at eworm dot de>
# Contributor: pmelo86 <patrick at patrickmelo dot eti dot br>

pkgname=lightdm-webkit-greeter-bzr
pkgver=13
pkgrel=4
pkgdesc="Webkit greeter for LightDM"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm-webkit-greeter"
license=('GPL3' 'LGPL3')
depends=('lightdm' 'webkitgtk2')
makedepends=('bzr' 'gnome-common' 'intltool')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
backup=("etc/lightdm/${pkgname%-*}.conf")
source=("${pkgname%-*}.conf")
sha256sums=('96c76474299ea286b39a9327b760393ca8f6bfb6065ae3eb118d22609bcf9980')

_bzrtrunk=https://code.launchpad.net/~lightdm-team/lightdm-webkit-greeter/trunk
_bzrmod=lightdm-webkit-greeter

build() {
  cd "${srcdir}"

# Pull
  msg "Connecting to Bazaar server...."

  if [[ -d ${_bzrmod} ]]; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_bzrmod}-build
  cp -r "${srcdir}"/${_bzrmod} "${srcdir}"/$_bzrmod-build
  cd "${srcdir}"/${_bzrmod}-build

# Build
  AUTOMAKE=automake ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/lightdm
  make
}

package() {
  cd "${srcdir}"/${_bzrmod}-build

# Install
  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/etc/lightdm
  install -m 644 ../${pkgname%-*}.conf "${pkgdir}"/etc/lightdm/${pkgname%-*}.conf
}

# vim: ts=2 sw=2 et:
