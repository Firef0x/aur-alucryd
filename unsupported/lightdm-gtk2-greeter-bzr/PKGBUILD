# Maintainer: Alucryd  <alucryd at gmail dot com>

pkgname=lightdm-gtk2-greeter-bzr
pkgver=92
pkgrel=1
pkgdesc="GTK+ greeter for LightDM"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm-gtk-greeter"
license=('GPL3' 'LGPL3')
depends=('gtk3' 'lightdm')
makedepends=('bzr' 'exo' 'gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
provides=('lightdm-gtk-greeter')
conflicts=('lightdm-gtk3-greeter')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
source=('lightdm-gtk-greeter::bzr+http://bazaar.launchpad.net/~lightdm-gtk-greeter-team/lightdm-gtk-greeter/trunk/')
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/lightdm-gtk-greeter

  bzr revno
}

build() {
  cd "${srcdir}"/lightdm-gtk-greeter

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/lightdm --disable-static --with-gtk2
  make
}

package() {
  cd "${srcdir}"/lightdm-gtk-greeter

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
