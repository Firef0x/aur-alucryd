# Maintainer : Alucryd <alucryd at gmail dot com>

pkgname=nvidia-source-beta
pkgver=313.18
pkgrel=3
pkgdesc="NVIDIA kernel module source (DKMS) - BETA version"
arch=('i686' 'x86_64')
[[ $CARCH == i686 ]] && _arch=x86 && _pkg=NVIDIA-Linux-${_arch}-${pkgver} && md5sums=('780c37c28a6e06e9571cafe348b7da64')
[[ $CARCH == x86_64 ]] && _arch=x86_64 && _pkg=NVIDIA-Linux-${_arch}-${pkgver}-no-compat32 && md5sums=('fa17a260793a38b4b8ae367db2e03b39')
url="http://www.nvidia.com/"
license=('custom')
depends=('linux>=3.7' 'linux<3.8' "nvidia-utils-beta=${pkgver}" 'dkms')
provides=("nvidia=${pkgver}" "nvidia-source=${pkgver}")
conflicts=('nvidia-beta' 'nvidia-source')
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run" 'linux-3.7.7.patch')
md5sums+=('e396280ac707cd19211c589fbe07b3d2')
install=${pkgname}.install

build() {
  cd "${srcdir}"
  if [[ -d ${_pkg} ]] ; then
    rm -rf ${_pkg}
  fi
  sh ${_pkg}.run --extract-only
}

package() {
  cd "${srcdir}"/${_pkg}/kernel

# Fix for kernel >= 3.7.7
  patch -Np2 -i "${srcdir}"/linux-3.7.7.patch

# Install files
  install -dm 755 "${pkgdir}"/usr/{src/nvidia-${pkgver},share/licenses}
  cp -dr --no-preserve=ownership * "${pkgdir}"/usr/src/nvidia-${pkgver}/
  ln -s /usr/share/licenses/nvidia "${pkgdir}"/usr/share/licenses/nvidia-source-beta

# Fix permissions
  chmod 644 "${pkgdir}"/usr/src/nvidia-${pkgver}/*
  chmod 755 "${pkgdir}"/usr/src/nvidia-${pkgver}/*.sh
}

# vim: ts=2 sw=2 et:
