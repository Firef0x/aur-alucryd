# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=light-locker-git
pkgver=1.1.0.r2129.b31ec27
pkgrel=1
pkgdesc='A simple locker forked from gnome-screensaver'
arch=('i686' 'x86_64')
url="https://github.com/the-cavalry/light-locker"
license=('GPL2')
depends=('dbus-glib' 'gtk3' 'libxss' 'libxxf86misc' 'lightdm')
makedepends=('git' 'gnome-common' 'intltool')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "%s.r%s.%s" "$(git describe --tags | sed 's/^v//')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build () {
  cd ${pkgname%-*}

  ./autogen.sh --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' --disable-{schemas-compile,static} --without-console-kit
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
