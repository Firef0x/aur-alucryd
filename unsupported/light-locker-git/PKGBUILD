# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=light-locker-git
pkgver=1.4.0.r5.f45fdd4
pkgrel=1
pkgdesc='A simple session locker'
arch=('i686' 'x86_64')
url='https://github.com/the-cavalry/light-locker'
license=('GPL2')
depends=('dbus-glib' 'gtk3' 'libxss' 'libxxf86misc' 'lightdm')
makedepends=('git' 'gnome-common' 'intltool')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "%s" "$(git describe --tags | sed 's/-/.r/; s/-g/./; s/^v//')"
}

build () {
  cd ${pkgname%-*}

  ./autogen.sh --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' --disable-{schemas-compile,static} --with-{mit-ext,systemd,xf86gamma-ext,x} --without-{console-kit,upower}
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
