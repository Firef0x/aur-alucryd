# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=rbutil-git
pkgver=1.4.0.r471.5fb3702
pkgrel=1
pkgdesc='Rockbox Utility'
arch=('i686' 'x86_64')
url='http://www.rockbox.org/twiki/bin/view/Main/RockboxUtility'
license=("GPL")
depends=('libusbx' 'qt5-base')
makedepends=('gendesk' 'git' 'qt5-tools')
optdepends=('speex: voice files support')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::git://git.rockbox.org/rockbox")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  printf "1.4.0.r%s.%s" "$(git rev-list rbutil_1.4.0..HEAD --count)" "$(git rev-parse --short HEAD)"
}

prepare() {
  gendesk -n --pkgname "${pkgname%-*}" --pkgdesc "${pkgdesc}" --name="Rockbox Utility" --exec="RockboxUtility" --categories="Application;Utility;" --comment="Rockbox automated installer"
}

build() {
  cd ${pkgname%-*}/${pkgname%-*}/${pkgname%-*}qt

  lrelease-qt5 rbutilqt.pro
  qmake-qt5
  make
}

package(){
  cd ${pkgname%-*}/${pkgname%-*}/${pkgname%-*}qt

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,pixmaps}}
  install -m 755 RockboxUtility "${pkgdir}"/usr/bin/
  install -m 644 icons/rockbox-64.png "${pkgdir}"/usr/share/pixmaps/rbutil.png
#  install -m 644 "${srcdir}"/rbutil.desktop "${pkgdir}"/usr/share/applications/rbutil.desktop
}

# vim: ts=2 sw=2 et:
