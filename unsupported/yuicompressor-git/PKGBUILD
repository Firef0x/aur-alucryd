# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=yuicompressor-git
pkgver=2.4.8.r15.f71090d
pkgrel=1
pkgdesc='The Yahoo! JavaScript and CSS Compressor'
arch=('any')
url='http://yui.github.io/yuicompressor/'
license=('BSD')
depends=('java-runtime')
makedepends=('apache-ant')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("git+https://github.com/yui/${pkgname%-*}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "%s" "$(git describe --tags | sed 's/\([^-]*-\)g/r\1/; s/-/./g; s/^v//')"
}

build() {
  cd ${pkgname%-*}

  ant
}

check() {
  cd ${pkgname%-*}

  ./tests/suite.sh
}

package() {
  cd ${pkgname%-*}

  install -dm 755 "${pkgdir}"/usr/{bin,share/{java/${pkgname%-*},licenses/${pkgname}}}
  install -m 755 build/${pkgname%-*}-${pkgver/.r*/}.jar "${pkgdir}"/usr/share/java/${pkgname%-*}/${pkgname%-*}.jar
  install -m 644 LICENSE.TXT "${pkgdir}"/usr/share/licenses/${pkgname}/

  echo '#!/bin/sh
  "$JAVA_HOME/bin/java" -jar /usr/share/java/yuicompressor/yuicompressor.jar $@' > "${pkgdir}"/usr/bin/${pkgname%-*}
  chmod +x "${pkgdir}"/usr/bin/${pkgname%-*}
}

# vim: ts=2 sw=2 et:
