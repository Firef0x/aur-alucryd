# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Mathieu Clabaut <mathieu dot clabaut at gmail dot com>

pkgname=taskjuggler3
pkgver=3.5.0
pkgrel=2
pkgdesc="Project Management Software"
arch=('any')
url="http://www.taskjuggler.org"
license=('GPL')
depends=('ruby-mail>=2.4.3' 'ruby-term-ansicolor>=1.0.7')
source=("http://rubygems.org/downloads/${pkgname%3}-${pkgver}.gem")
noextract=("${pkgname%3}-${pkgver}.gem")
sha256sums=('42f2e81470be9b2486fc074ba6ff04180258f462fed5c46cba871b7518cd0465')

build() {
  gem install --no-user-install --ignore-dependencies -i . ${pkgname%3}-${pkgver}.gem
}

package() {
  local _rubyver="$(ruby --version | sed 's/.* \(.*\..*\..*\)p.*/\1/')"
  install -dm 755 "${pkgdir}"/usr/{lib/ruby/{gems,vendor_ruby}/${_rubyver},share/{doc,vim/vimfiles/syntax}}
  mv {gems/${pkgname%3}-${pkgver},"${pkgdir}"/usr}/bin
  mv gems/${pkgname%3}-${pkgver}/lib/* "${pkgdir}"/usr/lib/ruby/vendor_ruby/${_rubyver}/
  mv gems/${pkgname%3}-${pkgver}/data "${pkgdir}"/usr/share/${pkgname%3}
  mv gems/${pkgname%3}-${pkgver}/manual "${pkgdir}"/usr/share/doc/${pkgname%3}
  mv doc specifications "${pkgdir}"/usr/lib/ruby/gems/${_rubyver}/
  mv "${pkgdir}"/usr/share/{${pkgname%3},vim/vimfiles/syntax}/tjp.vim
  rm "${pkgdir}"/usr/lib/ruby/vendor_ruby/${_rubyver}/{header.tmpl,updateheader.sh}
}

# vim: ts=2 sw=2 et:
