# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=firewalld-git
pkgver=0.3.9.3.1.r29.8b01bca
pkgrel=1
pkgdesc='A firewall daemon with D-BUS interface providing a dynamic firewall'
arch=('any')
url='http://fedorahosted.org/firewalld'
license=('GPL2')
depends=('ebtables' 'gtk3' 'iptables' 'libnotify' 'networkmanager' 'python-slip')
makedepends=('docbook-xsl' 'git' 'intltool')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=('git+http://git.fedorahosted.org/git/firewalld.git'
        'firewalld-arch.patch')
sha256sums=('SKIP'
            '91dbd5c580d3603e77c905880ca99ebddf398cf72f86ad37d0134ff7aa119ced')

pkgver() {
  cd ${pkgname%-*}

  printf "%s" "$(git describe --tags | sed 's/^r//; s/_/./g; s/-/./; s/-/.r/; s/-g/./')"
}

prepare() {
  cd ${pkgname%-*}

  patch -Np1 -i ../firewalld-arch.patch
}

build () {
  cd ${pkgname%-*}

  ./autogen.sh
  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' --disable-schemas-compile
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
