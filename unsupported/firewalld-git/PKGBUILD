# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=firewalld-git
pkgver=20130311
pkgrel=1
pkgdesc="Firewall service daemon that provides a dynamic customizable firewall with a D-BUS interface"
arch=('any')
url="http://fedorahosted.org/firewalld"
license=('GPL2')
depends=('ebtables' 'gtk3' 'hicolor-icon-theme' 'iptables' 'libnotify' 'networkmanager' 'python2-slip')
makedepends=('git' 'intltool')
backup=('etc/conf.d/firewalld' 'etc/firewalld/firewalld.conf')
provides=('firewalld')
conflicts=('firewalld')
install=${pkgname}.install
source=('paths.patch')
sha256sums=('5bbb24a6f5fb18c771e0b20b31545a1c8f32e2fe222b5188a8e27ee068bdb658')

_gitroot="http://git.fedorahosted.org/git/firewalld.git"
_gitname="firewalld"

build () {
  cd "${srcdir}"

# Clone
  msg "Connecting to GIT server...."

  if [[ -d ${_gitname} ]]; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_gitname}-build
  git clone "${srcdir}"/${_gitname} "${srcdir}"/${_gitname}-build
  cd "${srcdir}"/${_gitname}-build

# Patch
  patch -Np1 -i "${srcdir}"/paths.patch
  sed -i 's|sysconfig|conf\.d|' config/firewalld.service
  sed -i 's|^#!.*python$|#!/usr/bin/python2|
          s|^#!.*python -Es$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
  sed -i 's|python|python2|g' fix_python_shebang.sh

# Build
  PYTHON=python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}"/${_gitname}-build

# Install
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
