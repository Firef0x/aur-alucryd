# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=lutris
pkgver=0.3.4
pkgrel=1
pkgdesc='Open Gaming Platform'
arch=('any')
url='http://lutris.net/'
license=('GPL3')
depends=('dconf' 'desktop-file-utils' 'hicolor-icon-theme' 'polkit' 'python2-gobject' 'python2-xdg' 'python2-yaml')
makedepends=('python2-setuptools')
install="${pkgname}.install"
source=("${url}/releases/${pkgname}_${pkgver}.tar.gz")
sha256sums=('b817cce83c9149b9110a96e2372624091a47fd3a2d2c5eba749b0f1d3a3fa1db')

prepare() {
  cd ${pkgname}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

package() {
  cd ${pkgname}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
