# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=lutris
pkgver=0.3.5
pkgrel=1
pkgdesc='Open Gaming Platform'
arch=('any')
url='http://lutris.net/'
license=('GPL3')
depends=('dconf' 'desktop-file-utils' 'hicolor-icon-theme' 'polkit'
         'python2-gobject' 'python2-xdg' 'python2-yaml')
makedepends=('python2-setuptools')
install="${pkgname}.install"
source=("${url}/releases/${pkgname}_${pkgver}.tar.gz")
sha256sums=('d8eca9a7d287860fff5273fec7005f9b3f8cd8cb5d6ce130f7dfc90d5ff3afa5')

prepare() {
  cd ${pkgname}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

package() {
  cd ${pkgname}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
