# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>

pkgname=python2-slip
pkgver=0.2.24
pkgrel=2
pkgdesc="Simple Library for Python"
arch=('any')
url="http://fedorahosted.org/python-slip"
license=('GPL2')
depends=('gtk2' 'polkit' 'python2-dbus' 'python2-gobject' 'python2-decorator' 'pygtk')
source=("http://fedorahosted.org/released/python-slip/python-slip-${pkgver}.tar.bz2" 'python2.patch')
sha256sums=('ffd4c1beca05b0af9f30923e9304c97e714e2c7e535ff4ccee0486d2d7e7e08b'
            'eefadda7f605ffad5e74050889876cedade82ba4910264266174852d61d2a8df')

build() {
  cd "${srcdir}"

# Python 2
  patch -Np1 -i python2.patch 

# Build
  cd python-slip-${pkgver}
  make
}

package() {
  cd "${srcdir}"/python-slip-${pkgver}

# Install
  make DESTDIR="${pkgdir}" install

# Fix permissions
  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
}

# vim: ts=2 sw=2 et:
