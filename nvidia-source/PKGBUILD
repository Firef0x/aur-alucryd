# Maintainer : Alucryd <alucryd at gmail dot com>

pkgname=nvidia-source
pkgver=310.19
pkgrel=5
pkgdesc="NVIDIA kernel module source (DKMS)"
arch=('i686' 'x86_64')
[ $CARCH = x86_64 ] && _arch=x86_64 && _srcname=NVIDIA-Linux-x86_64-${pkgver}-no-compat32 && md5sums=('0ba08d32852e442ebba5ba22c7abed36')
[ $CARCH = i686 ] && _arch=x86 && _srcname=NVIDIA-Linux-x86-${pkgver} && md5sums=('2adbdd38540b3a8955714760e05f575d')
url="http://www.nvidia.com/"
license=('custom')
depends=('linux>=3.7' 'linux<3.8' "nvidia-utils=${pkgver}" 'dkms')
provides=("nvidia=${pkgver}")
conflicts=('nvidia' 'nvidia-source-beta')
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_srcname}.run")
install=${pkgname}.install

build() {
  cd "${srcdir}"
  if [ -d ${_srcname} ] ; then
    rm -rf ${_srcname}
  fi
  sh ${_srcname}.run --extract-only
}

package() {
  cd "${srcdir}"

# Install files
  install -dm 755 "${pkgdir}"/usr/{src/nvidia-${pkgver},share/licenses}
  cp -dr --no-preserve=ownership ${_srcname}/kernel/* "${pkgdir}"/usr/src/nvidia-${pkgver}/
  ln -s /usr/share/licenses/nvidia "${pkgdir}"/usr/share/licenses/nvidia-source

# Fix permissions
  chmod 644 "${pkgdir}"/usr/src/nvidia-${pkgver}/*[!.sh]
  chmod 755 "${pkgdir}"/usr/src/nvidia-${pkgver}/*.sh
}

# vim: ts=2 sw=2 et:
