# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=midori-granite
pkgver=0.5.7
pkgrel=1
pkgdesc='A lightweight, fast, and free web browser'
arch=('i686' 'x86_64')
url='http://midori-browser.org/'
license=('LGPL2.1')
depends=('gcr' 'granite' 'libnotify' 'libxss' 'libzeitgeist' 'webkitgtk')
makedepends=('cmake' 'intltool' 'librsvg' 'vala')
optdepends=('aria2: Download utility'
            'gst-plugins-base: HTML5 OGG videos support'
            'gst-plugins-good: HTML5 H264 and WebM videos support'
            'gst-libav: HTML5 H264 videos support')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!emptydirs')
install="${pkgname}.install"
source=("${url}/downloads/midori_${pkgver}_all_.tar.bz2")
sha256sums=('2875bb2aa8a8a0cce342178f04700d4ebccd13bf1b2ad4cb3ba9fec7e2bd0b4d')

build() {
  cd ${pkgname%%-*}-${pkgver}

  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_{LIBDIR='/usr/lib',PREFIX='/usr'} -DUSE_{GRANITE,GTK3}='ON'
  make
}

package() {
  cd ${pkgname%%-*}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
