# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-calculator-bzr
pkgver=r79
pkgrel=1
pkgdesc='A tiny, simple calculator written in GTK+ and Vala'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-calculator'
license=('GPL3')
depends=('granite-bzr')
makedepends=('bzr' 'cmake' 'vala')
provides=('pantheon-calculator')
conflicts=('pantheon-calculator')
install='pantheon-calculator.install'
source=('bzr+lp:pantheon-calculator')
sha256sums=('SKIP')

pkgver() {
  cd pantheon-calculator

  printf "r%s" "$(bzr revno)"
}

build() {
  cd pantheon-calculator

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd pantheon-calculator/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
