# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=maya-calendar-bzr
pkgver=r523
pkgrel=1
pkgdesc='GCal-syncing GTK+ calendar application'
arch=('i686' 'x86_64')
url='https://launchpad.net/maya'
license=('GPL3')
depends=('evolution-data-server' 'geocode-glib' 'granite-bzr' 'libchamplain' 'libpeas')
makedepends=('bzr' 'cmake' 'vala')
install="${pkgname%-*}.install"
source=("${pkgname%-*}::bzr+lp:maya")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

  sed 's|${CMAKE_INSTALL_FULL_LIBDIR}|${CMAKE_INSTALL_PREFIX}/lib|g' -i $(grep -rl CMAKE_INSTALL_FULL_LIBDIR)
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
