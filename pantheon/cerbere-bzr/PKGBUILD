# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=cerbere-bzr
pkgver=r47
pkgrel=1
pkgdesc='The Pantheon Watchdog'
arch=('i686' 'x86_64')
url='https://launchpad.net/cerbere'
license=('GPL2')
depends=('dconf' 'libgee')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=('bzr+lp:cerbere')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

# vim: ts=2 sw=2 et:
package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
