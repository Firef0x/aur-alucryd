# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: fresh24 <pascher.christian@gmail.com>

pkgname=gtk-theme-elementary-bzr
pkgver=r165
pkgrel=1
pkgdesc="elementary GTK theme"
arch=('any')
url='https://launchpad.net/egtk'
license=('GPL3')
depends=('gtk-engine-murrine' 'gtk-engine-unico')
makedepends=('bzr')
optdepends=('elementary-icon-theme')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::bzr+lp:egtk")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

package() {
  cd ${pkgname%-*}

  mkdir -p "${pkgdir}"/usr/share/themes/elementary
  cp -dr --no-preserve=ownership gtk-{2,3}.0 metacity-1 index.theme "${pkgdir}"/usr/share/themes/elementary/

# Fix permissions
  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
}

# vim: ts=2 sw=2 et:
