# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=elementary-icon-theme-bzr
pkgver=r1175
pkgrel=1
pkgdesc="elementary icon theme"
arch=('any')
url="https://launchpad.net/elementaryicons"
license=('GPL3')
depends=('gtk-update-icon-cache' 'hicolor-icon-theme')
makedepends=('bzr')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!emptydirs')
install="${pkgname%-*}.install"
source=("${pkgname%-*}::bzr+lp:elementaryicons")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

package() {
  rm -rf ${pkgname%-*}/{.bzr*,AUTHORS,CONTRIBUTORS,COPYING,icons/elementary/icon-theme.cache}
  install -dm 755 "${pkgdir}"/usr/share/icons
  cp -dr --no-preserve=ownership ${pkgname%-*} "${pkgdir}"/usr/share/icons/elementary
}

# vim: ts=2 sw=2 et:

