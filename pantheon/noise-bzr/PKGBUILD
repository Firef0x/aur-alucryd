# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=noise-bzr
pkgver=1327
pkgrel=1
pkgdesc="The official audio player of elementary OS - BZR version"
arch=('i686' 'x86_64')
url="https://launchpad.net/noise"
license=('GPL3')
groups=('pantheon')
depends=('desktop-file-utils' 'granite-bzr' 'gstreamer0.10-base' 'json-glib' 'libgpod' 'libindicate' 'libnotify' 'libpeas' 'libsoup' 'sqlheavy-git' 'taglib' 'zeitgeist')
makedepends=('bzr' 'cmake' 'vala>=0.16')
provides=('noise')
conflicts=('noise')
install=${pkgname}.install

_bzrtrunk=https://code.launchpad.net/~elementary-apps/noise/trunk
_bzrmod=noise

build() {
  cd "${srcdir}"

# Pull
  msg "Connecting to Bazaar server...."

  if [[ -d ${_bzrmod} ]]; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_bzrmod}-build
  cp -r "${srcdir}"/${_bzrmod} "${srcdir}"/$_bzrmod-build
  cd "${srcdir}"/${_bzrmod}-build

# Build
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd "${srcdir}"/${_bzrmod}-build/build

# Install
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}
