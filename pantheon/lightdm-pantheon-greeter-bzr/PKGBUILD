# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter-bzr
pkgver=r275
pkgrel=1
pkgdesc='Pantheon greeter for LightDM'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-greeter'
license=('GPL')
depends=('clutter-gtk' 'granite-bzr' 'libindicator-gtk3' 'lightdm')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!emptydirs')
install="${pkgname%-*}.install"
source=("${pkgname%-*}::bzr+lp:pantheon-greeter"
        'lightdm.profile'
        '00-pantheon-greeter'
        'desktop.patch')
sha256sums=('SKIP'
            '3e13948d1fe7055ab411875ff717094941f8bbf86bdbec51b490faab60460729'
            '1db0281fd8852861d1e07f75ea384ded5f0e557a8a5de1f71deced089610c3a3'
            '6a2712b4c5087330f9ef1f5f3a62307b1330cabae183d10b02f665c149ba0dc8')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

  patch -Np0 -i ../desktop.patch
}

build(){
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package(){
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/bin
  mv "${pkgdir}"/usr/{sbin/,bin/lightdm-}pantheon-greeter
  mv "${pkgdir}"/usr/share/xgreeters/{,lightdm-}pantheon-greeter.desktop

  # dconf
  install -dm 755 "${pkgdir}"/etc/dconf/{db/lightdm.d,profile}
  install -m 644 ../../lightdm.profile "${pkgdir}"/etc/dconf/profile/lightdm
  install -m 644 ../../00-pantheon-greeter "${pkgdir}"/etc/dconf/db/lightdm.d/
}

# vim: ts=2 sw=2 et:
