# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=contractor-bzr
pkgver=r133
pkgrel=1
pkgdesc='A service for sharing data between apps'
arch=('i686' 'x86_64')
url='https://launchpad.net/contractor'
license=('GPL3')
depends=('libgee')
makedepends=('bzr' 'cmake' 'seahorse' 'vala')
optdepends=('brasero: Burn Disc context menu entries'
            'file-roller: Archive context menu entries'
            'gnome-bluetooth: Bluetooth context-menu entry'
            'inkscape: Inkscape context menu entry'
            'seahorse: Seahorse integration'
            'webcontracts-bzr: Contracts for web services')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('bzr+lp:contractor'
        'bzr+lp:~elementary-os/contractor/brasero-contracts'
        'bzr+lp:~elementary-os/contractor/inkscape-contracts'
        'bzr+lp:~elementary-os/contractor/seahorse-contracts')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install

  for _app in brasero inkscape seahorse; do
    install -m 644 "${srcdir}"/${_app}-contracts/*.contract "${pkgdir}"/usr/share/contractor/
  done
  install -dm 755 "${pkgdir}"/usr/lib/svg-contracts
  install -m 755 "${srcdir}"/inkscape-contracts/inkscape-export.sh "${pkgdir}"/usr/lib/svg-contracts
}

# vim: ts=2 sw=2 et:
