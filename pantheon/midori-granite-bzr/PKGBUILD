# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=midori-granite-bzr
pkgver=5845
pkgrel=1
pkgdesc='Lightweight web browser based on Gtk WebKit'
arch=('i686' 'x86_64')
url="https://launchpad.net/midori"
license=('LGPL2.1')
depends=('desktop-file-utils' 'granite-bzr' 'hicolor-icon-theme' 'libnotify' 'libunique' 'libwebkit' 'libxss' 'libzeitgeist' 'webkitgtk3')
makedepends=('libxml2' 'intltool' 'gtk-doc' 'librsvg' 'python2' 'vala' 'gcr')
optdepends=('gstreamer0.10-ugly-plugins: HTML5 videos support'
            'aria2: download utility')
options=('!emptydirs')
install=${pkgname}.install

_bzrtrunk=https://code.launchpad.net/~vcs-imports/midori/master
_bzrmod=midori

build() {
 cd "${srcdir}"

# Pull
  msg "Connecting to Bazaar server...."

  if [[ -d ${_bzrmod} ]]; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_bzrmod}-build
  cp -r "${srcdir}"/${_bzrmod} "${srcdir}"/$_bzrmod-build
  cd "${srcdir}"/${_bzrmod}-build

# Patch
  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" wscript waf

# Build
  PYTHON=python2 ./configure --prefix=/usr --jobs=${MAKEFLAGS/-j/} --enable-addons --enable-unique --enable-nls --enable-docs --enable-apidocs
  make
}

package() {
  cd "${srcdir}"/${_bzrmod}-build

# Install
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
