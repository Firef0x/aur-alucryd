# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=midori-granite-bzr
pkgver=r6557
pkgrel=1
pkgdesc='A lightweight, fast, and free web browser'
arch=('i686' 'x86_64')
url='http://midori-browser.org/'
license=('LGPL2.1')
depends=('gcr' 'granite-bzr' 'libnotify' 'libxss' 'libzeitgeist' 'webkitgtk')
makedepends=('bzr' 'cmake' 'intltool' 'librsvg' 'vala')
optdepends=('aria2: Download utility'
            'gst-plugins-base: HTML5 OGG videos support'
            'gst-plugins-good: HTML5 H264 and WebM videos support'
            'gst-libav: HTML5 H264 videos support')
provides=("${pkgname%%-*}")
conflicts=("${pkgname%%-*}")
options=('!emptydirs')
install="${pkgname%%-*}.install"
source=('bzr+lp:midori')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%%-*}

  printf "r%s" "$(bzr revno)"
}

build() {
  cd ${pkgname%%-*}

  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_{LIBDIR='/usr/lib',PREFIX='/usr'} -DUSE_{GRANITE,GTK3}='ON'
  make
}

package() {
  cd ${pkgname%%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
