# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=gala-bzr
pkgver=r413
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/gala'
license=('GPL3')
depends=('clutter-gtk' 'granite' 'mutter' 'plank-bzr')
makedepends=('bzr' 'gnome-common' 'intltool' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=("${pkgname%-*}::bzr+http://bazaar.launchpad.net/~gala-dev/gala/trunk/")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

build() {
  cd ${pkgname%-*}

  ./autogen.sh --prefix='/usr' --disable-schemas-compile
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
