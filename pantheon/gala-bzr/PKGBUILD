# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=gala-bzr
pkgver=r423
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/gala'
license=('GPL3')
depends=('clutter-gtk' 'granite' 'mutter' 'plank')
makedepends=('bzr' 'gnome-common' 'intltool' 'vala')
provides=('gala')
conflicts=('gala')
install='gala.install'
#source=('gala::bzr+http://bazaar.launchpad.net/~gala-dev/gala/trunk/')
source=('gala-mutter314::bzr+http://bazaar.launchpad.net/~gala-dev/gala/mutter314/'
        'gala-notifications.patch')
sha256sums=('SKIP'
            '172bda2235da4e9e3b96b920c37f894d5453e05dff01897c1ca3e39a3b940b53')

pkgver() {
#  cd gala
  cd gala-mutter314

  printf "r%s" "$(bzr revno)"
}

prepare() {
#  cd gala
  cd gala-mutter314

  patch -Np0 -i ../gala-notifications.patch
}

build() {
#  cd gala
  cd gala-mutter314

  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
#  cd gala
  cd gala-mutter314

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
