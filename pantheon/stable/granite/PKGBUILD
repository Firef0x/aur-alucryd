# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=granite
pkgver=0.2.1
pkgrel=1
pkgdesc="An extension of GTK+ libraries"
arch=('i686' 'x86_64')
url="https://launchpad.net/granite"
license=('LGPL3')
depends=('gtk3' 'hicolor-icon-theme' 'libgee06')
makedepends=('cmake' 'gobject-introspection' 'vala')
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/${pkgver%\.?}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('4e9a97488c04b2c3eadcb4b8251c8938a9637820b1989449e55490f42a2ac92c')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
