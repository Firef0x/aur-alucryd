# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=granite
pkgver=0.1.1
pkgrel=2
pkgdesc="An extension of GTK+ libraries"
arch=('i686' 'x86_64')
url="https://launchpad.net/granite"
license=('GPL3')
depends=('gtk3' 'hicolor-icon-theme' 'libgee06')
makedepends=('cmake' 'gobject-introspection' 'vala')
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/0.1/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('360a030690d3db1faedf7461fe808b37ed67de7f380532673dae2242f2e7b5e5')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DICON_UPDATE=OFF
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
