# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=slingshot-launcher
pkgver=0.7.2
pkgrel=1
pkgdesc="Slingshot application launcher"
arch=('i686' 'x86_64')
url="https://launchpad.net/slingshot"
license=('GPL3')
depends=('gnome-menus2' 'granite-bzr' 'libzeitgeist')
makedepends=('vala' 'cmake')
install=${pkgname}.install
source=("https://launchpad.net/${pkgname%-*}/0.x/${pkgver}/+download/${pkgname%-*}-${pkgver}.tar.gz")
sha256sums=('144d28ec330df97e479189679e67a0f797c1f7d79625245bd1995df5b64da900')

build() {
  cd ${pkgname%-*}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DCMAKE_EXE_LINKER_FLAGS="-lm"
  make
}

package() {
  cd ${pkgname%-*}-${pkgver}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
