# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=slingshot-launcher
pkgver=0.6
pkgrel=1
pkgdesc="Slingshot application launcher"
arch=('i686' 'x86_64')
url="https://launchpad.net/slingshot"
license=('GPL3')
depends=('gnome-menus2' 'granite-bzr' 'libzeitgeist')
makedepends=('vala' 'cmake')
install=${pkgname}.install
source=("https://launchpad.net/${pkgname%-*}/trunk/${pkgver}/+download/${pkgname%-*}-${pkgver}.tar.bz2")
sha256sums=('968f3d11cfde081089564c01e919a56bc73cf71b35df3bcc03261e410c58b5c7')

build() {
  cd "${srcdir}"/${pkgname%-*}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DICONCACHE_UPDATE=OFF -DCMAKE_EXE_LINKER_FLAGS="-lm"
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}-${pkgver}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
