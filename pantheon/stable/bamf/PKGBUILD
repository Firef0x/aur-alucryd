# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=bamf
pkgver=0.3.6
pkgrel=2
pkgdesc="Application matching framework"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('dbus-glib' 'libgtop' 'libwnck3')
makedepends=('gobject-introspection' 'vala')
options=('!libtool')
source=("http://launchpad.net/${pkgname}/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        'xdg-current-desktop.patch')
sha256sums=('df7c7e3635329e0bc702941ec9b0d026b773017e85eb6c62c9a2af9f8957aa7a'
            '8673df57480b6e033f3379d7eb20be4d5757b44e530346c933cfdb559b096215')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  patch -Np1 -i ../xdg-current-desktop.patch
  sed -i 's/--c-include/-include/' lib/libbamf/Makefile.in
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname} --disable-static --disable-webapps
  make
}

package() {
  cd "${srcdir}"/${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
