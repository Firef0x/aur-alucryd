# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=bamf
pkgver=0.4.0
pkgrel=1
pkgdesc="Application matching framework"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('dbus-glib' 'libgtop' 'libwnck3')
makedepends=('gobject-introspection' 'vala')
options=('!libtool')
source=("http://launchpad.net/${pkgname}/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        'bamf-matcher.patch')
sha256sums=('65d05e0173336a8548bf5f67a940b46f02eb000bf7dc60a05b13fb9ccd7aade4'
            'f55e92d0a99e1b1ab005a4b39d9d6b62e64d447353c20a92b1bb822a7aafa35c')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ../bamf-matcher.patch
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname} --disable-{static,webapps}
  make
}

package() {
  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
