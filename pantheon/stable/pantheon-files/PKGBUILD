# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=pantheon-files
pkgver=0.1.2
pkgrel=1
pkgdesc="Files is a simple, powerful, sexy file manager for the Pantheon desktop."
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-files"
license=('GPL3')
depends=('gconf' 'desktop-file-utils' 'granite' 'libnotify')
makedepends=('bzr' 'cmake' 'gnome-common' 'sqlite3' 'vala')
conflicts=('marlin-bin' 'marlin-bzr')
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tgz")
sha256sums=('3d7dbb2b8a0b6dc8a2bac34acb235c8899663b0db83cb66850dd4e5c683998e6')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
