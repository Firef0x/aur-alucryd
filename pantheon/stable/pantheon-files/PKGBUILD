# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=pantheon-files
pkgver=0.1.1
pkgrel=1
pkgdesc="Files is a simple, powerful, sexy file manager for the Pantheon desktop."
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-files"
license=('GPL3')
depends=('gconf' 'desktop-file-utils' 'granite' 'libnotify')
makedepends=('bzr' 'cmake' 'gnome-common' 'sqlite3' 'vala')
conflicts=('marlin-bin' 'marlin-bzr')
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/1.x/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('e4ca68d4f94c6f2bd725ca2058b0bd560eeb5eb196e3883bb964a4e8b64f0e34')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
