# Maitainer: Alucryd <alucryd at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libindicate-gtk3
pkgver=12.10.1
pkgrel=2
pkgdesc="Small library for applications to raise flags on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
depends=('dbus-glib' 'libdbusmenu-gtk3' 'python2-gobject2')
makedepends=('gnome-common-git' 'gnome-doc-utils' 'gobject-introspection' 'gtk3' 'gtk-doc' 'gtk-sharp-2' 'perl-xml-libxml' 'pygtk' 'vala')
provides=('libindicate')
conflicts=('libindicate')
options=('!libtool' '!emptydirs')
source=("http://launchpad.net/libindicate/${pkgver%.*}/${pkgver}/+download/${pkgname%-*}-${pkgver}.tar.gz" 'headers.patch' 'python2.patch' 'mono.patch')
sha256sums=('41e0c059e96172975c716aa70dc60f669e21d359c65935c61ac374a9aa075581'
            '6316ce002bd79278623bba915677a0e94086c5b35536ee8b8e3684dafe9bdf98'
            '3e6185455aa1901bc97eaca7222995a112c0acfec35d096140c9a13a2b5ca7cf'
            'acf06964f453cca3627c9e4d73d1fcdd3713497316566910ab7522f8297ddcd6')

build() {
  cd "${srcdir}"/${pkgname%-*}-${pkgver}

# Patch
  patch -Np2 -i "${srcdir}"/headers.patch
  patch -Np2 -i "${srcdir}"/python2.patch
  patch -Np2 -i "${srcdir}"/mono.patch

# Build
  PYTHON=python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname} --disable-static --with-gtk3
  make -j1
}

package() {
  cd "${srcdir}"/${pkgname%-*}-${pkgver}

# Install
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
