# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=noise
pkgver=0.2
pkgrel=3
pkgdesc="The official audio player of elementary OS"
arch=('i686' 'x86_64')
url="https://launchpad.net/noise"
license=('GPL3')
groups=('pantheon')
depends=('desktop-file-utils' 'granite-bzr' 'gstreamer0.10-base' 'json-glib' 'libgpod' 'libindicate' 'libnotify' 'libpeas' 'libsoup' 'sqlheavy' 'taglib' 'zeitgeist')
makedepends=('cmake' 'vala>=0.16')
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/0.x/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f5cbf73f36ad68e6a491cdc72a4e4a9d68bbdc7e3e66ee205cdcf33bd48f66a4')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
