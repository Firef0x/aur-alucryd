# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libdbusmenu-gtk2
pkgver=12.10.2
pkgrel=2
pkgdesc="Small little library that passes a menu structure across DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/libdbusmenu"
license=('LGPL')
depends=('libdbusmenu')
makedepends=('gnome-doc-utils' 'gobject-introspection' 'intltool' 'vala')
options=('!libtool' '!emptydirs')
source=("http://launchpad.net/dbusmenu/${pkgver%.*}/${pkgver}/+download/${pkgname%-*}-${pkgver}.tar.gz" 'headers.patch')
sha256sums=('9d6ad4a0b918b342ad2ee9230cce8a095eb601cb0cee6ddc1122d0481f9d04c9'
            'fcdb77001aa6ebfd4d91bc3bc532b819a96db191fb8046f83aed0f8849d1ee0a')

build() {
  cd "${srcdir}"/${pkgname%-*}-${pkgver}

# Patch
  patch -Np1 -i "${srcdir}"/headers.patch
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/dbusmenu-bench

# Build
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname%-*} --disable-static --with-gtk=2
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}-${pkgver}

# Install
  make -C libdbusmenu-gtk DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
