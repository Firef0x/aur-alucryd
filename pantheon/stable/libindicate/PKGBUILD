# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Branchini Massimo < max dot bra dot gtalk at gmail dot com>

pkgname=libindicate
pkgver=12.10.1
pkgrel=3
pkgdesc="Library for raising indicators via DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
depends=('dbus-glib' 'libdbusmenu' 'python2-gobject2')
makedepends=('gnome-doc-utils' 'gobject-introspection' 'gtk-doc' 'gtk-sharp-2' 'perl-xml-libxml' 'pygtk' 'vala')
options=('!libtool' '!emptydirs')
source=("http://launchpad.net/libindicate/${pkgver%.*}/${pkgver}/+download/${pkgname%-*}-${pkgver}.tar.gz" 'mono.patch')
sha256sums=('41e0c059e96172975c716aa70dc60f669e21d359c65935c61ac374a9aa075581'
            'acf06964f453cca3627c9e4d73d1fcdd3713497316566910ab7522f8297ddcd6')

build() {
  cd "${srcdir}"/${pkgname%-*}-${pkgver}

# Patch
  patch -Np2 -i "${srcdir}"/mono.patch
  sed -i 's|^#!.*python$|#!/usr/bin/python2|' examples/{im-client,listen-and-print}.py
  sed -i 's|pyglib-2.0-python$PYTHON_VERSION|pyglib-2.0-python2|' configure

# Build
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname} --disable-static
  make -j1
}

package() {
  cd "${srcdir}"/${pkgname%-*}-${pkgver}

# Install
  make -j1 DESTDIR="${pkgdir}" install
  make -j1 -C libindicate-gtk DESTDIR="${pkgdir}" uninstall
}

# vim: ts=2 sw=2 et:
