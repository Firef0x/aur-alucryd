# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=pantheon-session-bzr
pkgver=61
pkgrel=1
pkgdesc='Session entry for Pantheon'
arch=('any')
url='https://code.launchpad.net/~elementary-os/elementaryos/pantheon-xsession-settings'
license=('GPL3')
depends=('cerbere' 'gala-bzr' 'gconf' 'gnome-keyring' 'gnome-session' 'gnome-settings-daemon' 'gnome-user-share' 'plank' 'polkit-gnome' 'slingshot-launcher' 'wingpanel' 'xdg-user-dirs-gtk')
makedepends=('bzr')
source=("${pkgname%-*}::bzr+http://bazaar.launchpad.net/~elementary-os/elementaryos/pantheon-xsession-settings/"
        'pantheon-session.sh')
sha256sums=('SKIP'
            '1e0c8e9403e52614d43ecb920e507ee1e96b1439bb20949ff814741e5e5e2284')

pkgver() {
  cd ${pkgname%-*}

  bzr revno
}

prepare() {
  cd ${pkgname%-*}

  sed 's/policykit-1/polkit/' -i autostart/polkit-gnome-authentication-agent-1-pantheon.desktop
  sed 's|gnome-session --session=pantheon|/usr/bin/pantheon-session|' -i debian/pantheon.desktop
}

package() {
  cd ${pkgname%-*}

  mkdir -p "${pkgdir}"/{etc/xdg,usr/{bin,share/{gnome-session/sessions,pantheon,xsessions}}}
  mv autostart "${pkgdir}/etc/xdg/"
  mv debian/pantheon.desktop "${pkgdir}/usr/share/xsessions/"
  mv debian/pantheon.session "${pkgdir}/usr/share/gnome-session/sessions/"
  mv gconf "${pkgdir}/usr/share/GConf"
  mv unity-greeter "${pkgdir}/usr/share/"
  mv applications "${pkgdir}/usr/share/pantheon/"

# Fix permissions
  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +

# gnome-session workaround
  install -m 755 ../pantheon-session.sh "${pkgdir}"/usr/bin/pantheon-session
}

# vim: ts=2 sw=2 et:
