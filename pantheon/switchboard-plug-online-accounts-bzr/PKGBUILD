# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-online-accounts-bzr
pkgver=r138
pkgrel=1
pkgdesc='Online Accounts plug for Switchboard'
arch=('i686' 'x86_64')
url='https://launchpad.net/switchboard-plug-onlineaccounts'
license=('GPL3')
depends=('credentials-control-center' 'gsignond-plugin-oauth'
         'gsignond-plugin-sasl' 'rest' 'switchboard-bzr' 'webkit2gtk')
makedepends=('bzr' 'cmake' 'vala')
provides=('switchboard-plug-online-accounts')
conflicts=('switchboard-plug-online-accounts')
install='switchboard-plug-online-accounts.install'
source=('switchboard-plug-online-accounts::bzr+http://bazaar.launchpad.net/~elementary-apps/switchboard-plug-onlineaccounts/trunk/')
sha256sums=('SKIP')

pkgver() {
  cd switchboard-plug-online-accounts

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd switchboard-plug-online-accounts

  sed 's/webkit2gtk-3.0/webkit2gtk-4.0/' -i CMakeLists.txt
}

build() {
  cd switchboard-plug-online-accounts

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make -j1
}

package() {
  cd switchboard-plug-online-accounts/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
