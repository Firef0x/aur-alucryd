# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=switchboard-plug-online-accounts-bzr
pkgver=r41
pkgrel=1
pkgdesc='Online Accounts plug for Switchboard'
arch=('i686' 'x86_64')
url='https://launchpad.net/switchboard-plug-onlineaccounts'
license=('GPL3')
depends=('gsignond-plugin-oauth' 'gsignond-plugin-sasl' 'libaccounts-glib' 'libgsignon-glib' 'switchboard-bzr')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::bzr+http://bazaar.launchpad.net/~elementary-apps/switchboard-plug-onlineaccounts/trunk/")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make pantheon-online-accounts
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
