# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-online-accounts-bzr
pkgver=r138
pkgrel=1
pkgdesc='Online Accounts plug for Switchboard'
arch=('i686' 'x86_64')
url='https://launchpad.net/switchboard-plug-onlineaccounts'
license=('GPL3')
depends=('credentials-control-center' 'gsignond-plugin-oauth-git'
         'gsignond-plugin-sasl-git' 'rest' 'switchboard-bzr' 'webkit2gtk2.4')
makedepends=('bzr' 'cmake' 'vala')
provides=('switchboard-plug-online-accounts')
conflicts=('switchboard-plug-online-accounts')
install='switchboard-plug-online-accounts.install'
source=('switchboard-plug-online-accounts::bzr+http://bazaar.launchpad.net/~elementary-apps/switchboard-plug-onlineaccounts/trunk/')
sha256sums=('SKIP')

pkgver() {
  cd switchboard-plug-online-accounts

  printf "r%s" "$(bzr revno)"
}

build() {
  cd switchboard-plug-online-accounts

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make -j1
}

package() {
  cd switchboard-plug-online-accounts/build

  make -j1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
