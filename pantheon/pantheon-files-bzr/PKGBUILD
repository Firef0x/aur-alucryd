# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=pantheon-files-bzr
pkgver=1315
pkgrel=1
pkgdesc="The file manager of the Pantheon desktop"
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-files"
license=('GPL3')
depends=('gconf' 'desktop-file-utils' 'granite' 'libnotify')
makedepends=('bzr' 'cmake' 'gnome-common' 'sqlite3' 'vala0.16')
optdepends=('contractor-bzr: Plugins integration'
            "${pkgname%-*}-plugin-dropbox-bzr: Dropbox integration"
            'tumbler: Thumbnails generation')
provides=("${pkgname%-*}")
conflicts=('marlin-bin' 'marlin-bzr' "${pkgname%-*}")
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  bzr revno
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DVALA_EXECUTABLE=/usr/bin/valac-0.16 -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}

# vim: ts=2 sw=2 et:
