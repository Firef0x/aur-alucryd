# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=snap-photobooth-bzr
pkgver=r232
pkgrel=1
pkgdesc='Snap webcam viewer'
arch=('i686' 'x86_64')
url='https://launchpad.net/snap-elementary'
license=('GPL3')
depends=('granite-bzr' 'gst-plugins-base-libs' 'zeitgeist')
makedepends=('bzr' 'cmake' 'vala')
install="${pkgname%-*}.install"
source=("${pkgname%-*}::bzr+lp:snap-elementary")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
