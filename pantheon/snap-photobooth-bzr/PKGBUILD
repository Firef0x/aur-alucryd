# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=snap-photobooth-bzr
pkgver=r232
pkgrel=1
pkgdesc='Snap webcam viewer'
arch=('i686' 'x86_64')
url='https://launchpad.net/snap-elementary'
license=('GPL3')
depends=('granite' 'gst-plugins-base-libs')
makedepends=('bzr' 'cmake' 'vala' 'zeitgeist')
install='snap-photobooth.install'
source=('snap-photobooth::bzr+lp:snap-elementary')
sha256sums=('SKIP')

pkgver() {
  cd snap-photobooth

  printf "r%s" "$(bzr revno)"
}

build() {
  cd snap-photobooth

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='FALSE'
  make
}

package() {
  cd snap-photobooth/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
