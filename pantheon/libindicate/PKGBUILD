# Maitainer: Alucryd <alucryd at gmail dot com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libindicate
pkgver=12.10.1
pkgrel=2
pkgdesc="Small library for applications to raise flags on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
depends=('dbus-glib' 'libdbusmenu')
makedepends=('gnome-doc-utils' 'gobject-introspection' 'gtk3' 'gtk-sharp-2' 'perl-xml-libxml' 'pygtk' 'python2-gobject2' 'vala')
options=('!libtool' '!emptydirs')
source=("http://launchpad.net/libindicate/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
md5sums=('aeed68ec8047a3325b4aa4aef38f010a')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

# Python 2
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' examples/{im-client,listen-and-print}.py

# Fixes
  sed -i '/gmessages\.h/ d' libindicate/indicator.c
  sed -i 's/pyglib-2.0-python$PYTHON_VERSION/pyglib-2.0-python2/' configure
  sed -i 's|indicate-sharp-0.1|mono/indicate-sharp|' bindings/mono/indicate/indicate-sharp-0.1.pc.in
  sed -i 's|indicate-gtk-sharp-0.1|mono/indicate-gtk-sharp|' bindings/mono/indicate-gtk/indicate-gtk-sharp-0.1.pc.in
  sed -i 's|$(libdir)/$(ASSEMBLY_NAME)-sharp-0.1|$(libdir)/mono/$(ASSEMBLY_NAME)-sharp|
          s|-package $(ASSEMBLY_NAME) |-package $(ASSEMBLY_NAME)-sharp |' bindings/mono/indicate{,-gtk}/Makefile.in

# Build
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname} --disable-static
  make -j1
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

# Install
  make -C libindicate DESTDIR="${pkgdir}" install
  make -C bindings/mono/indicate DESTDIR="${pkgdir}" install
  make -C docs DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
