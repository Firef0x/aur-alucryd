# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=switchboard-plug-power-bzr
pkgver=r103
pkgrel=1
pkgdesc='Power plug for Switchboard'
arch=('i686' 'x86_64')
url='https://launchpad.net/switchboard-plug-power'
license=('GPL3')
depends=('switchboard-bzr')
makedepends=('bzr' 'cmake' 'vala')
source=('bzr+lp:switchboard-plug-power'
        'no-lid-gsettings.patch')
sha256sums=('SKIP'
            'f79f770f50999d4e552d2bba29d8bcdf676e23236e7842003069d925e61d8fd5')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

  patch -Np1 -i ../no-lid-gsettings.patch
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
