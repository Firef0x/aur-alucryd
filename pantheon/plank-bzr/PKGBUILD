# Maintainer: Alucryd <alucryd at gmail dot com> 

pkgname=plank-bzr
pkgver=758
pkgrel=1
pkgdesc="A stupidly simple dock from elementary"
arch=('i686' 'x86_64')
url="https://launchpad.net/plank"
license=('GPL3')
groups=('pantheon')
depends=('bamf-bzr' 'libdbusmenu-gtk3' 'libgee')
makedepends=('bzr' 'gnome-common-git' 'intltool'  'vala>=0.18')
options=('!libtool')
install=${pkgname}.install

_bzrtrunk=https://code.launchpad.net/~docky-core/plank/trunk
_bzrmod=plank

build() {
  cd "${srcdir}"

# Pull
  msg "Connecting to Bazaar server...."

  if [[ -d ${_bzrmod} ]]; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_bzrmod}-build
  cp -r "${srcdir}"/${_bzrmod} "${srcdir}"/$_bzrmod-build
  cd "${srcdir}"/${_bzrmod}-build

# Build
  ./autogen.sh --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}"/${_bzrmod}-build

# Install
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
