# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic@gmail.com>

pkgname=gsignond
pkgver=1.0.1
pkgrel=1
pkgdesc='gSSO glib daemon'
arch=('i686' 'x86_64')
url='https://01.org/gsso'
license=('LGPL2.1')
depends=('dbus' 'libgsignon-glib' 'sqlite')
makedepends=('git' 'gobject-introspection' 'gtk-doc')
backup=('etc/gsignond.conf')
source=("${pkgname%-*}::git+https://code.google.com/p/accounts-sso.gsignond/#commit=61538c810dbe")
sha256sums=('SKIP')

build() {
  cd ${pkgname%-*}

  mkdir -p m4
  gtkdocize
  aclocal
  autoheader
  libtoolize --copy --force
  autoconf
  automake --add-missing --copy
  autoreconf --install --force
  ./configure --prefix='/usr' --sysconfdir='/etc' --enable-dbus-type='session'
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
