# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-terminal-bzr
pkgver=r716
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-terminal'
license=('GPL3')
depends=('desktop-file-utils' 'granite-bzr' 'libnotify' 'vte290')
makedepends=('bzr' 'cmake' 'vala')
provides=('pantheon-terminal')
conflicts=('pantheon-terminal')
install='pantheon-terminal.install'
source=('pantheon-terminal::bzr+lp:pantheon-terminal'
        'pantheon-terminal-bash-notify.patch')
sha256sums=('SKIP'
            'ea1d2257c0bedc11ec6616a5bab35a9a37d270c0e7d492692f057c9801d7a865')

pkgver() {
  cd pantheon-terminal

  echo "r$(bzr revno)"
}

prepare() {
  cd pantheon-terminal

  patch -Np1 -i ../pantheon-terminal-bash-notify.patch
}

build() {
  cd pantheon-terminal

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='FALSE'
  make
}

package() {
  cd pantheon-terminal/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
