# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-terminal-bzr
pkgver=r705
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-terminal'
license=('GPL3')
depends=('desktop-file-utils' 'granite-bzr' 'libnotify' 'vte290')
makedepends=('bzr' 'cmake' 'vala')
provides=('pantheon-terminal')
conflicts=('pantheon-terminal')
install='pantheon-terminal.install'
source=('pantheon-terminal::bzr+lp:pantheon-terminal'
        'bash-notify.patch')
sha256sums=('SKIP'
            'a92e7207e8f9961316ae90610e4d90e464352337454b7633cb131751ec1dad3a')

pkgver() {
  cd pantheon-terminal

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd pantheon-terminal

  #patch -Np2 -i ../bash-notify.patch
}

build() {
  cd pantheon-terminal

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='FALSE'
  make
}

package() {
  cd pantheon-terminal/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
