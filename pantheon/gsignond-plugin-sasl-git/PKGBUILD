# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gsignond-plugin-sasl-git
pkgver=r29.55f1e28
pkgrel=1
pkgdesc='SASL plugin for gSSO'
arch=('i686' 'x86_64')
url='https://01.org/gsso'
license=('LGPL2.1')
depends=('gsasl' 'gsignond')
makedepends=('git' 'gobject-introspection' 'gtk-doc')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::git+https://code.google.com/p/accounts-sso.gsignond-plugin-sasl/")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-*}

  mkdir -p m4
  gtkdocize
  aclocal
  autoheader
  libtoolize --copy --force
  autoconf
  automake --add-missing --copy
  autoreconf --install --force
  ./configure --prefix='/usr' --enable-gtk-doc
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
