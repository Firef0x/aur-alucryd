# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-default-settings-bzr
pkgver=r112
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=('any')
url='https://code.launchpad.net/~elementary-os/elementaryos/default-settings-trusty'
license=('GPL')
groups=('pantheon')
depends=('elementary-icon-theme' 'gtk-theme-elementary-bzr' 'gnome-themes-standard' 'pantheon-backgrounds-bzr' 'plank-theme-pantheon-bzr' 'ttf-droid' 'ttf-opensans')
makedepends=('bzr')
install="${pkgname%-*}.install"
source=("${pkgname%-*}::bzr+lp:~elementary-os/elementaryos/default-settings-trusty"
        'arch-tweaks.patch')
sha256sums=('SKIP'
            'c3aaa3a30fff349b2ce8c2dd3e6e505e0e53bf5d01dedd60e40cfdc9751205ca')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

  patch -Np1 -i ../arch-tweaks.patch
}

package() {
  cd ${pkgname%-*}

  install -dm 755 "${pkgdir}"/{etc/{gtk-3.0,skel/.config,xdg},usr/share/glib-2.0/schemas}
  install -m 644 debian/elementary-default-settings.gsettings-override "${pkgdir}"/usr/share/glib-2.0/schemas/25_${pkgname%-*}.gschema.override
  install -m 644 settings.ini "${pkgdir}"/etc/gtk-3.0/
  cp -dr --no-preserve='ownership' plank "${pkgdir}"/etc/skel/.config/
  cp -dr --no-preserve='ownership' midori "${pkgdir}"/etc/xdg/
}

# vim: ts=2 sw=2 et:
