# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=switchboard-plug-default-applications-bzr
pkgver=59
pkgrel=1
pkgdesc="Deafult Applications plug for Switchboard"
arch=('i686' 'x86_64')
url="https://launchpad.net/switchboard-plug-default-applications"
license=('GPL3')
depends=('libpantheon-bzr' 'switchboard-bzr')
makedepends=('bzr' 'cmake' 'vala')
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
