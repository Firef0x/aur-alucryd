# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=gala-bzr
pkgver=323
pkgrel=1
pkgdesc="A window & compositing manager based on libmutter and designed by elementary"
arch=('i686' 'x86_64')
url="https://launchpad.net/gala"
license=('GPL3')
groups=('pantheon')
depends=('clutter-gtk' 'granite-bzr' 'mutter' 'plank-bzr')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DCMAKE_EXE_LINKER_FLAGS=-lm
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
