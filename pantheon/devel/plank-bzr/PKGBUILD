# Maintainer: Alucryd <alucryd at gmail dot com> 

pkgname=plank-bzr
pkgver=820
pkgrel=1
pkgdesc="Elegant, simple, clean dock"
arch=('i686' 'x86_64')
url="https://launchpad.net/plank"
license=('GPL3')
groups=('pantheon')
depends=('bamf' 'libdbusmenu-gtk3' 'libgee')
makedepends=('bzr' 'gnome-common' 'intltool')
provides=('plank')
conflicts=('plank')
options=('!libtool')
install=${pkgname}.install
source=('bzr+lp:plank')
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/plank

  bzr revno
}

build() {
  cd "${srcdir}"/plank

  ./autogen.sh --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}"/plank

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
