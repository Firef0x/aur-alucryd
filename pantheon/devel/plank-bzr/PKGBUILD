# Maintainer: Alucryd <alucryd at gmail dot com> 
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>
# Contributor: dcelasun <dcelasun@gmail.com>

pkgname=plank-bzr
pkgver=860
pkgrel=1
pkgdesc="Elegant, simple, clean dock"
arch=('i686' 'x86_64')
url="https://launchpad.net/plank"
license=('GPL3')
depends=('bamf' 'hicolor-icon-theme' 'libdbusmenu-gtk3' 'libgee06')
makedepends=('bzr' 'gnome-common' 'intltool' 'vala0.16')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!libtool')
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  bzr revno
}

prepare() {
  cd ${pkgname%-*}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd ${pkgname%-*}

  export VALAC=/usr/bin/valac-0.16
  export VAPIGEN=/usr/bin/vapigen-0.16
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-gee-0.8
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}

# vim: ts=2 sw=2 et:
