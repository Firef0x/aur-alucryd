# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=pantheon-files-bzr
pkgver=1133
pkgrel=1
pkgdesc="A simple, powerful, sexy file manager for the Pantheon desktop"
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-files"
license=('GPL3')
groups=('pantheon')
depends=('dbus-glib' 'gconf' 'desktop-file-utils' 'granite-bzr' 'libnotify')
makedepends=('bzr' 'cmake' 'gnome-common-git' 'sqlite3' 'vala>=0.16')
optdepends=('tumbler: thumbnails generation'
            'contractor-bzr: plugins integration'
            'pantheon-files-plugin-dropbox-bzr: Dropbox integration')
provides=('pantheon-files')
conflicts=('marlin-bin' 'marlin-bzr' 'files-bzr' 'pantheon-files')
install=${pkgname}.install

_bzrtrunk=https://code.launchpad.net/~elementary-apps/pantheon-files/trunk
_bzrmod=pantheon-files

build() {
  cd "${srcdir}"

# Pull
  msg "Connecting to Bazaar server...."

  if [[ -d ${_bzrmod} ]]; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_bzrmod}-build
  cp -r "${srcdir}"/${_bzrmod} "${srcdir}"/$_bzrmod-build
  cd "${srcdir}"/${_bzrmod}-build

# Build
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DICON_UPDATE=OFF

  make
}

package() {
  cd "${srcdir}"/${_bzrmod}-build/build

# Install
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
