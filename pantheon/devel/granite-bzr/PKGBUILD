# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=granite-bzr
pkgver=561
pkgrel=1
pkgdesc="A development library for elementary development"
arch=('i686' 'x86_64')
url="https://launchpad.net/granite"
license=('GPL3')
groups=('pantheon')
depends=('gtk3' 'hicolor-icon-theme' 'libgee06')
makedepends=('bzr' 'cmake' 'gobject-introspection' 'vala>=0.16')
provides=('granite')
conflicts=('granite')
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
