# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=granite-bzr
pkgver=543
pkgrel=1
pkgdesc="A development library for elementary development"
arch=('i686' 'x86_64')
url="https://launchpad.net/granite"
license=('GPL3')
groups=('pantheon')
depends=('gtk3' 'hicolor-icon-theme' 'libgee')
makedepends=('bzr' 'cmake' 'gobject-introspection' 'vala>=0.16')
provides=('granite')
conflicts=('granite')
install=${pkgname}.install

_bzrtrunk=https://code.launchpad.net/~elementary-pantheon/granite/granite
_bzrmod=granite

build() {
  cd "$srcdir"

# Pull
  msg "Connecting to Bazaar server...."

  if [[ -d ${_bzrmod} ]]; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_bzrmod}-build
  cp -r "${srcdir}"/${_bzrmod} "${srcdir}"/$_bzrmod-build
  cd "${srcdir}"/${_bzrmod}-build

# Build
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DICON_UPDATE=OFF
  make
}

package() {
  cd "${srcdir}"/$_bzrmod-build/build

# Install
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
