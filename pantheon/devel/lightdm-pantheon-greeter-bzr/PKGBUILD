# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: sh4nks <sh4nks7 at gmail dot com

pkgname=lightdm-pantheon-greeter-bzr
pkgver=163
pkgrel=1
pkgdesc="Pantheon greeter for LightDM"
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-greeter"
license=('GPL')
depends=('clutter-gtk' 'granite-bzr' 'libindicator3' 'lightdm')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("${pkgname%-*}::bzr+lp:pantheon-greeter")
sha256sums=('SKIP')

build(){
  cd "$srcdir"/${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package(){
  cd "$srcdir"/${pkgname%-*}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/sbin
  mv "${pkgdir}"/usr/{share/xgreeters,sbin}/pantheon-greeter
  sed -i 's|share/xgreeters|sbin|
          s|pantheon-greeter|lightdm-pantheon-greeter|g
          /en_US/d' "${pkgdir}"/usr/share/xgreeters/pantheon-greeter.desktop
  mv "${pkgdir}"/usr/share/xgreeters/{,lightdm-}pantheon-greeter.desktop
}

# vim: ts=2 sw=2 et:
