# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: sh4nks <sh4nks7 at gmail dot com

pkgname=lightdm-pantheon-greeter-bzr
pkgver=163
pkgrel=1
pkgdesc="Pantheon greeter for LightDM"
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-greeter"
license=('GPL')
groups=('pantheon')
depends=('clutter-gtk' 'granite-bzr' 'libindicator-gtk3' 'lightdm' 'numlockx')
makedepends=('bzr' 'cmake' 'vala>=0.16')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("${pkgname%-*}::bzr+https://code.launchpad.net/~elementary-pantheon/pantheon-greeter/1.x")
sha256sums=('SKIP')

build(){
  cd "$srcdir"/${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  export CFLAGS="$CFLAGS -lm -Wno-deprecated-declarations"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package(){
  cd "$srcdir"/${pkgname%-*}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
