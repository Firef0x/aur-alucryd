# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=contractor-bzr
pkgver=105
pkgrel=1
pkgdesc="A service for sharing data between apps"
arch=('i686' 'x86_64')
url="https://launchpad.net/contractor"
license=('GPL3')
depends=('libgee')
makedepends=('bzr' 'cmake' 'vala>=0.16')
provides=('contractor')
conflicts=('contractor')
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
