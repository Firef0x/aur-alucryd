# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=footnote-bzr
pkgver=287
pkgrel=1
pkgdesc="A beautiful, fast, and simple note taking app in the style of elementary"
arch=('i686' 'x86_64')
url="https://launchpad.net/footnote"
license=('GPL3')
depends=('clutter-gtk' 'granite-bzr' 'sqlheavy-git')
makedepends=('bzr' 'cmake' 'vala>=0.16')
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

prepare() {
  cd "${srcdir}"/${pkgname%-*}

  sed -i 's|sqlheavy-0.1|sqlheavy-0.2|' CMakeLists.txt
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
