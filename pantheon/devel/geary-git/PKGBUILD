# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=geary-git
pkgver=0.3.1.48
pkgrel=1
pkgdesc="A lightweight email client for the GNOME desktop"
arch=('i686' 'x86_64')
url="http://www.yorba.org/projects/geary/"
license=('GPL3')
groups=('pantheon')
depends=('desktop-file-utils' 'gmime' 'hicolor-icon-theme' 'libcanberra' 'libgee' 'libnotify' 'libunique3' 'webkitgtk3')
makedepends=('cmake' 'git' 'gobject-introspection' 'intltool' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("git://yorba.org/${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  git describe | sed 's/.*-\(.*-.*\)-.*/\1/; s/-/./'
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  ./configure --prefix=/usr --disable-{desktop-update,icon-update,schemas-compile}
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
