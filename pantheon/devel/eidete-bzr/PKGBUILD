# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=eidete-bzr
pkgver=144
pkgrel=1
pkgdesc="A simple screencasting app from the elementary project"
arch=('i686' 'x86_64')
url="https://launchpad.net/eidete"
license=('GPL3')
depends=('granite-bzr' 'gstreamer0.10-base' 'libwnck3')
makedepends=('bzr' 'cmake' 'vala>=0.16')
optdepends=('contractor: contractor support'
            'brasero: burn captured video to disc')
provides=('eidete')
conflicts=('eidete')
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_EXE_LINKER_FLAGS="-lX11"
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
