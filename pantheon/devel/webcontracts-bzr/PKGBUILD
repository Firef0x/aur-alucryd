# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=webcontracts-bzr
pkgver=24
pkgrel=1
pkgdesc="Standalone contracts for web services"
arch=('i686' 'x86_64')
url="https://launchpad.net/webcontracts"
license=('GPL3')
depends=('granite-bzr' 'json-glib' 'libpantheon-bzr' 'rest' 'webkitgtk3')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
