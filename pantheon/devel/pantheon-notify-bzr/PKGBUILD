# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-notify-bzr
pkgver=25
pkgrel=1
pkgdesc="Stylish and standard-compliant notification daemon"
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-notify"
license=('GPL3')
depends=('clutter-gtk' 'granite-bzr' 'libwnck3')
makedepends=('bzr' 'cmake' 'vala')
source=("bzr+lp:${pkgname%-*}")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}/build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
