# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Dustin Mays <dork dot fish dot wat at gmail dot com>

pkgname=bamf-bzr
pkgver=529
pkgrel=1
pkgdesc="Window matching library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('dbus-glib' 'libgtop' 'libwnck3')
makedepends=('bzr' 'gnome-common' 'gobject-introspection' 'gtk-doc' 'vala')
provides=('bamf')
conflicts=('bamf')
options=('!libtool')
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}

  bzr revno
}

build() {
  cd "${srcdir}"/${pkgname%-*}

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname%-*} --disable-{static,webapps}
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
