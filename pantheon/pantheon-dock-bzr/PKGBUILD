# Maintainer: Maxime Gauduin <alucryd@gmail.com> 

pkgname=pantheon-dock-bzr
pkgver=r962
pkgrel=1
pkgdesc="The dock for elementary Pantheon, built on the awesome foundation of Plank"
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgname%-*}"
license=('GPL3')
depends=('bamf' 'libdbusmenu-gtk3' 'libgee06')
makedepends=('bzr' 'gnome-common' 'intltool' 'vala')
provides=("${pkgname%-*}" 'plank')
conflicts=("${pkgname%-*}" 'plank')
install="${pkgname%-*}.install"
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd ${pkgname%-*}

  ./autogen.sh --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' --disable-gee-0.8
  make
}

package() {
  cd "${srcdir}"/${pkgname%-*}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/share/apport}
}

# vim: ts=2 sw=2 et:
