# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: flamelab <panosfilip at gmail dot com>

pkgname=slingshot-launcher-bzr
pkgver=396
pkgrel=1
pkgdesc='Slingshot application launcher'
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgname%%-*}"
license=('GPL3')
depends=('gnome-menus2' 'granite-bzr' 'libunity' 'libwnck3' 'zeitgeist')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=("${pkgname%-*}::bzr+lp:${pkgname%%-*}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  bzr revno
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
