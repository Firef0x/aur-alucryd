# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=scratch-text-editor-bzr
pkgver=r1414
pkgrel=1
pkgdesc='The Pantheon Text Editor'
arch=('i686' 'x86_64')
url='https://launchpad.net/scratch'
license=('GPL3')
depends=('desktop-file-utils' 'granite-bzr' 'gtksourceview3' 'libpeas'
         'python2-chardet' 'zeitgeist')
makedepends=('bzr' 'cmake' 'gobject-introspection' 'vala' 'vte3' 'webkitgtk')
optdepends=('vala: Outline extension'
            'vte3: Terminal extension'
            'webkitgtk: Browser Preview extension')
provides=('scratch-text-editor')
conflicts=('scratch-text-editor')
install='scratch-text-editor.install'
source=('scratch-text-editor::bzr+lp:scratch'
        'scratch-vte291.patch')
sha256sums=('SKIP'
            '43bc03c7e21844ef2cc4697930c40494344ac0422a0963065f442321853079dd')

pkgver() {
  cd scratch-text-editor

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd scratch-text-editor

  sed -i 's/gtkspell-3.0/gtkspell3-3.0/' plugins/spell/CMakeLists.txt
  sed -i '/add_subdirectory (spell)/d' plugins/CMakeLists.txt
  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')

  patch -Np1 -i ../scratch-vte291.patch
}

build() {
  cd scratch-text-editor

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd scratch-text-editor/build

  make DESTDIR="${pkgdir}" install
  chmod +x "${pkgdir}"/usr/share/scratch/scripts/*.py
}

# vim: ts=2 sw=2 et:
