# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libindicator3
pkgver=12.10.1
pkgrel=2
pkgdesc="Shared functions for Ayatana indicators"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
depends=('gtk3')
options=('!libtool')
source=("https://launchpad.net/${pkgname%3}/${pkgver%.*}/${pkgver}/+download/${pkgname%3}-${pkgver}.tar.gz")
sha256sums=('b2d2e44c10313d5c9cd60db455d520f80b36dc39562df079a3f29495e8f9447f')

prepare() {
  cd "${srcdir}"/${pkgname%3}-${pkgver}

  sed -i 's/install-libindicatorincludeHEADERS install-pkgconfigDATA/install-pkgconfigDATA/g' libindicator/Makefile.in
  sed -i 's/gtk_icon_info_free/g_object_unref/' libindicator/indicator-image-helper.c
}

build() {
  cd "${srcdir}"/${pkgname%3}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${pkgname%3} --disable-{static,tests}
  make
}

package(){
  cd "${srcdir}"/${pkgname%3}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
