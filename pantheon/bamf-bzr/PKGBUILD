# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: dorkfish <dork.fish.wat@gmail.com>

pkgname=bamf-bzr
pkgver=r601
pkgrel=1
pkgdesc='Application matching framework'
arch=('i686' 'x86_64')
url='https://launchpad.net/bamf'
license=('GPL')
depends=('dbus-glib' 'libgtop' 'libwnck3' 'python2-lxml')
makedepends=('bzr' 'gnome-common' 'gobject-introspection' 'gtk-doc' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('bzr+lp:bamf')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' tests/gtester2xunit.py
}

build() {
  cd ${pkgname%-*}

  export PYTHON=/usr/bin/python2
  ./autogen.sh --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' \
               --libexecdir="/usr/lib/${pkgname%-*}" --disable-static
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
