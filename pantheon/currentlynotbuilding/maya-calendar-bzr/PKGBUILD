# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=maya-calendar-bzr
pkgver=415
pkgrel=1
pkgdesc="GCal-syncing GTK+ calendar application"
arch=('i686' 'x86_64')
url="https://launchpad.net/maya"
license=('GPL3')
depends=('evolution-data-server' 'glib2' 'granite-bzr' 'gtk3' 'libgee')
makedepends=('bzr' 'cmake' 'vala>=0.16')
install=${pkgname}.install

_bzrtrunk=https://code.launchpad.net/~elementary-apps/maya/trunk
_bzrmod=maya

build() {
cd "${srcdir}"

# Pull
  msg "Connecting to Bazaar server...."

  if [[ -d ${_bzrmod} ]]; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_bzrmod}-build
  cp -r "${srcdir}"/${_bzrmod} "${srcdir}"/$_bzrmod-build
  cd "${srcdir}"/${_bzrmod}-build



# Build
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd "${srcdir}"/${_bzrmod}-build/build
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
