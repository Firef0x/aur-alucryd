# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=geary-bzr
pkgver=876
pkgrel=1
pkgdesc="Lightweight email client for the GNOME desktop"
arch=('i686' 'x86_64')
url="https://launchpad.net/geary"
license=('GPL3')
groups=('pantheon')
depends=('desktop-file-utils' 'glib2' 'gobject-introspection' 'libgnome-keyring' 'gmime' 'granite-bzr' 'gtk3' 'libcanberra' 'libgee' 'libindicate-gtk3' 'libnotify' 'libunique3' 'sqlite' 'webkitgtk3')
makedepends=('bzr' 'cmake' 'intltool' 'vala>=0.18')
optdepends=('gnome-keyring: GNOME keyring support')
provides=('geary')
conflicts=('geary')
install=${pkgname}.install

_bzrtrunk=https://code.launchpad.net/~yorba/geary/master
_bzrmod=geary

build() {
  cd "${srcdir}"

# Pull
  msg "Connecting to Bazaar server...."

  if [[ -d ${_bzrmod} ]]; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_bzrmod}-build
  cp -r "${srcdir}"/${_bzrmod} "${srcdir}"/$_bzrmod-build
  cd "${srcdir}"/${_bzrmod}-build

# Build
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd "${srcdir}"/${_bzrmod}-build/build

# Install
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}

# vim: ts=2 sw=2 et:
