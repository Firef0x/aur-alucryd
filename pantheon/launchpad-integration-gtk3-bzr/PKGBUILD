# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=launchpad-integration-gtk3-bzr
pkgver=189
pkgrel=1
pkgdesc="A library to integrate launchpad urls into the menus"
arch=('i686' 'x86_64')
url="https://launchpad.net/launchpad-integration"
license=('GPL3')
depends=('hicolor-icon-theme' 'gtk3' 'pygtk')
makedepends=('bzr' 'gobject-introspection')
provides=('launchpad-integration-gtk3')
conflicts=('launchpad-integration-gtk3')
options=('!libtool')
install=${pkgname}.install

_bzrtrunk=https://code.launchpad.net/~ubuntu-desktop/launchpad-integration/ubuntu
_bzrmod=launchpad-integration

build() {
  cd "${srcdir}"

# Pull
  msg "Connecting to Bazaar server...."

  if [[ -d ${_bzrmod} ]]; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_bzrmod}-build
  cp -r "${srcdir}"/${_bzrmod} "${srcdir}"/$_bzrmod-build
  cd "${srcdir}"/${_bzrmod}-build

# Build
  PYTHON=python2 ./autogen.sh --prefix=/usr --disable-static --with-gtk=3
  make
}

package() {
  cd "${srcdir}"/${_bzrmod}-build

# Install
  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/lib/python2.7/site-packages
  mv "${pkgdir}"/usr/share/launchpad-integration "${pkgdir}"/usr/lib/python2.7/site-packages/
}

# vim: ts=2 sw=2 et:
