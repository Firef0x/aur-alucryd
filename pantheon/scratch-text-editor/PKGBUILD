# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>

pkgname=scratch-text-editor
pkgver=2.0.2
pkgrel=1
pkgdesc='The text editor that works'
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgname%%-*}"
license=('GPL3')
depends=('desktop-file-utils' 'granite' 'gtksourceview3' 'libpeas' 'python2-chardet' 'libzeitgeist')
makedepends=('cmake' 'gobject-introspection' 'vala' 'vte3' 'webkitgtk')
optdepends=('vala: Outline plugin'
            'vte3: Terminal plugin'
            'webkitgtk: Browser Preview plugin')
install="${pkgname}.install"
source=("${url}/2.x/${pkgver}/+download/${pkgname%%-*}-${pkgver}.tgz")
sha256sums=('65a341ce34c5d8cc119e3b582ac9027d83f91c01636527d55bbfc8b4cafd6a1e')

prepare() {
  cd ${pkgname%%-*}-${pkgver}

  sed -i 's/gtkspell-3.0/gtkspell3-3.0/' plugins/spell/CMakeLists.txt
  sed -i '/add_subdirectory (spell)/d' plugins/CMakeLists.txt
  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd ${pkgname%%-*}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname%%-*}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
  chmod +x "${pkgdir}"/usr/share/scratch/scripts/*.py
}

# vim: ts=2 sw=2 et:
