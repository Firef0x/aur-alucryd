# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-pantheon-shell-bzr
pkgver=r228
pkgrel=1
pkgdesc='Pantheon Shell plug for Switchboard'
arch=('i686' 'x86_64')
url='https://launchpad.net/switchboard-plug-pantheon-shell'
license=('GPL3')
depends=('switchboard-bzr')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname}")
conflicts=("${pkgname%-*}")
source=('bzr+lp:switchboard-plug-pantheon-shell'
        'arch-paths.patch')
sha256sums=('SKIP'
            '085777b30998596e521c83e0676687b6ca3e73f04d0f1a62c673f4a78b29c933')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

  patch -Np1 -i ../arch-paths.patch
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
