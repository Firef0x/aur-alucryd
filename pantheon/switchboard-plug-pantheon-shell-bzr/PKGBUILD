# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=switchboard-plug-pantheon-shell-bzr
pkgver=r194
pkgrel=1
pkgdesc='Pantheon Shell plug for Switchboard'
arch=('i686' 'x86_64')
url='https://launchpad.net/switchboard-plug-pantheon-shell'
license=('GPL3')
depends=('switchboard-bzr')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname}")
conflicts=("${pkgname%-*}")
source=('bzr+lp:switchboard-plug-pantheon-shell'
        'arch-tweaks.patch')
sha256sums=('SKIP'
            '43a3c4a7a18c75bf86681682b35a3191464f5885ef8d843da95d81b954e0ac93')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd ${pkgname%-*}

  patch -Np1 -i ../arch-tweaks.patch
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
