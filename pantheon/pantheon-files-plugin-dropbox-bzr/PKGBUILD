# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-files-plugin-dropbox-bzr
pkgver=r22
pkgrel=1
pkgdesc='Dropbox integration for pantheon-files'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-files'
license=('GPL3')
depends=('libgee06' 'pantheon-files')
makedepends=('bzr' 'cmake')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('bzr+lp:~elementary-apps/pantheon-files/pantheon-files-plugin-dropbox')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s" "$(bzr revno)"
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
