# Maintainer : Alucryd <alucryd at gmail dot com>

pkgname=nvidia-source-beta
pkgver=313.18
pkgrel=2
pkgdesc="NVIDIA kernel module source (DKMS) - BETA version"
arch=('i686' 'x86_64')
[ $CARCH = x86_64 ] && _arch=x86_64 && _srcname=NVIDIA-Linux-x86_64-${pkgver}-no-compat32 && md5sums=('fa17a260793a38b4b8ae367db2e03b39')
[ $CARCH = i686 ] && _arch=x86 && _srcname=NVIDIA-Linux-x86-${pkgver} && md5sums=('780c37c28a6e06e9571cafe348b7da64')
url="http://www.nvidia.com/"
license=('custom')
depends=('linux>=3.7' 'linux<3.8' "nvidia-utils-beta=${pkgver}" 'dkms')
provides=("nvidia=${pkgver}" "nvidia-source=${pkgver}")
conflicts=('nvidia-beta' 'nvidia-source')
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_srcname}.run")
install=${pkgname}.install

build() {
  cd "${srcdir}"
  if [ -d ${_srcname} ] ; then
    rm -rf ${_srcname}
  fi
  sh ${_srcname}.run --extract-only
}

package() {
  cd "${srcdir}"

# Install files
  install -dm 755 "${pkgdir}"/usr/{src/nvidia-${pkgver},share/licenses}
  cp -dr --no-preserve=ownership ${_srcname}/kernel/* "${pkgdir}"/usr/src/nvidia-${pkgver}/
  ln -s /usr/share/licenses/nvidia "${pkgdir}"/usr/share/licenses/nvidia-source-beta

# Fix permissions
  chmod 644 "${pkgdir}"/usr/src/nvidia-${pkgver}/*[!.sh]
  chmod 755 "${pkgdir}"/usr/src/nvidia-${pkgver}/*.sh
}

# vim: ts=2 sw=2 et:
